//(Regular Race Copy)

IMPORT {LIBRARY v2/1.10/minigames/races.ccu}

//<Initialize
FUNC {ACTIVATE CCU.minigames.races.init() ARGS}:
	DEF GLOBAL $SA$ x=450,y=0,z=-353,dx=124,dy=30,dz=45
	DEF GLOBAL $SPid$ 20

	DEF GLOBAL $CMDName$ ECR
	DEF GLOBAL $CMDSizeX$ 6
	DEF GLOBAL $CMDSizeY$ 6
	DEF GLOBAL $CMDSizeZ$ 16

	DEF GLOBAL $StyleOption$ +Z 6
	DEF GLOBAL COORDS $CoordsOption$ 443 10 -321
	DEF GLOBAL $BlockOption$ stonebrick 0
//>

//<Options
	OPTIONS:
		parseOption true
		commandOption true
		combinerOption true
	///	filePathFuncOption 
		parseChangesOption false
//>

//<Race specific options
	DEF $RaceType$ 1 /// Specifies it's a regular race as type 1
	DEF $Name$ EC /// Name
	DEF $TD$ ECR /// Text display name
	DEF $TDName$ Ever Changing Race /// Hover and scoreboard name
	DEF $Color$ yellow /// Scoreboard teams color
	DEF $Dao$ `` /// Place where air is when open
	DEF $Dro$ `` /// Place where redstone blocks are there when open

	DEF $lap$ x=565,y=3,z=-333,dy=3,dz=4 /// Where laps are completed
	DEF $lapAF$ x=497,y=0,z=-333,dy=4,dz=4 /// Area where laps are reset inside the races
	DEF $lapSP$ x=454,y=2,z=-334,dx=12,dy=9,dz=6 /// Area where laps are disabled in spawn
//>

//<ECR Options
///	Spawn Coords for teleporting
	DEF $SC$ 460 3 -331 -90 0

	DEF $StructureBlock$ structure_block 0 replace {mode:"LOAD",posZ:2,name:"ECRPlatform1",integrity:|0|}

	///	Smiley Heads coordinates
	DEF $SHead$ 466 4 -329 466 4 -333

	///	ECR Armor Stand placement for structure blocks
	ARRAY {COORDS Arr_StructurePlace}:
		467 3 -335
		499 3 -335
		531 3 -335

	///	Select Death
	DEF $ECRKill$ x=467,y=0,z=-333,dx=95,dy=-10,dz=4
	///	Coordinates of the platform
	DEF $ECRPlatform$ 467 2 -329 562 2 -333

	///	Percent list
	ARRAY {Arr_Percent}:
		5
		10
		15
		20
		25
		30
		35
		40
		45
		50
		100
		
	///	Custom death messages
	ARRAY {Arr_CustomDeathMessages}:
		experienced Smiley's wrath and spontaneously combusted
		accidentally dropped their car keys and jumped for it
		slipped on a banana
		realized they had lost some potential, but gained kinetic energy
		thought there was water underneath
		ignored the wet floor sign
		figured out what happens if you can't fly
		heard one of MisterBoy's puns
		had a fear of heights and looked down
		tried to be a gold block, but suddenly remembered they cannot float
		was banned from the server
		heard a panda roar
		thought they were playing a dropper map

	///	Portal
	DEF $Portal$ x=569,y=3,z=-333,dy=4,dz=4

	///	Barriers
	DEF $BarrierFill$ 466 6 -333 466 6 -329
//>

//<Race Edit Functions
	FUNC {Func_EditGeneralScoreboardStart}:
		USE {ScOA}:
			ECRSave dummy ECR Settings Save
			
		ARRAY {GLOBAL ACTIVATE CCU.objectiveAdd(Obj_EditGeneralScoreboardStart) Obj_EditGeneralScoreboardStart}:
			$Name$RaceDS dummy $TDName$ Delay State
			$Name$RaceTI dummy $TDName$ Delay Timer
			$Name$RaceFA deathCount Falls
			
		CCU_ExecuteSpawnStand ScOS belowName $Name$RaceFA
		CCU_ExecuteSpawnStand summon ArmorStand ~ ~ ~ {Marker:1,PersistenceRequired:1,Invisible:1,NoGravity:1,Tags:["ECRAS","ECRRaceTimer"]}
		CCU_ExecuteSpawnStand summon ArmorStand ~ ~ ~ {Marker:1,PersistenceRequired:1,Invisible:1,NoGravity:1,Tags:["ECRAS","ECRDeathMessages"]}
		
		LOOP {1 Arr_StructurePlace[L] + 1} {0 Arr_StructurePlace[-1] + 1}:
			CCU_ExecuteSpawnStand summon ArmorStand Arr_StructurePlace[|1|] \
					{Marker:1,PersistenceRequired:1,Invisible:1,NoGravity:1,Tags:["ECRAS","ECRStructure","ECRStructure|0|"]}
					
		@e[type=ArmorStand,ECRDeathMessages] $Name$RaceTI + 0
	
	FUNC {Func_EditGeneralScoreboardClock}:
	///	Portal
		@a[$Portal$,m=2,team=ECR] + ECRKilled
	
	///	Custom death messages
		@a[$ECRKill$,m=2,team=ECR,!ECRKilled] + ECRKilled
	///	kill @a[$ECRKill$,m=2,team=ECR,ECRKilled]
	///	@e[type=ArmorStand,ECRDeathMessages,$Name$RaceTI>=0<Arr_CustomDeathMessages[L]] $Name$RaceTI + 1
	///	@e[type=ArmorStand,ECRDeathMessages,$Name$RaceTI=Arr_CustomDeathMessages[L]] $Name$RaceTI = 0
		tp @a[$SA$,m=2,team=ECR,ECRKilled] $SC$
		@a[$SA$,m=2,team=ECR,ECRKilled] playsound Snd_TP voice @a[c=1]
		@a[$SA$,m=2,team=ECR,ECRKilled] particle happyVillager ~0.1 ~1 ~0.1 0.3 0.5 0.3 1 20 force
		effect @a[$SA$,m=2,team=ECR,ECRKilled] 8 5 255 true
		@a[$SA$,m=2,team=ECR,ECRKilled] $Name$RaceFA + 1
		@a[ECRKilled] - ECRKilled
		
	///	Fills stuff
		@e[type=ArmorStand,ECRRaceTimer,$Name$RaceTI>=1] $Name$RaceTI - 1
		testfor @e[type=ArmorStand,ECRRaceTimer,$Name$RaceTI=0,$Name$RaceDS=1]
		COND:
			ScOP @e[type=ArmorStand,ECRRaceTimer,$Name$RaceTI=0,$Name$RaceDS=1] $Name$RaceTI = Delay2 ECRSave
			@e[type=ArmorStand,ECRRaceTimer,$Name$RaceDS=1] $Name$RaceDS = 2
			CCU_Activate(Grp_StructureBlockPulse)
		
		@e[type=ArmorStand,ECRRaceTimer,$Name$RaceTI=0,$Name$RaceDS=2] fill $ECRPlatform$ air 0 replace gold_block 0
		@e[type=ArmorStand,ECRRaceTimer,$Name$RaceTI=0,$Name$RaceDS=2] fill $ECRPlatform$ gold_block 0 replace stained_glass 3
		
		testfor @e[type=ArmorStand,ECRRaceTimer,$Name$RaceTI=0,$Name$RaceDS=2]
		COND:
			ScOP @e[type=ArmorStand,ECRRaceTimer,$Name$RaceTI=0,$Name$RaceDS=2] $Name$RaceTI = Delay1 ECRSave
			@e[type=ArmorStand,ECRRaceTimer,$Name$RaceDS=2] $Name$RaceDS = 1
					
	FUNC {Func_DeathMessagesTFClock}:
		testfor @a[m=2,team=ECR,ECRKilled]
		CCU_SetblockTestfor
		
		LOOP {1 Arr_CustomDeathMessages[L] + 1}:
			@e[type=ArmorStand,ECRDeathMessages,$Name$RaceTI=|0|] @a[m=2,team=ECR,ECRKilled] \
					tellraw @a $TextStart${"selector":"@a[c=1,m=2,ECRKilled]"},{"text":" Arr_CustomDeathMessages[|0|]"}]}
			
		@a[ECRKilled] - ECRKilled
		
	FUNC {Func_StructureBlockPulse}:
		ScOP @e[type=ArmorStand,ECRRaceTimer] ECRSave = Percent ECRSave
		LOOP {1 Arr_Percent[L] + 1} {0 Arr_Percent[-1] + 1}:
			@e[type=ArmorStand,ECRRaceTimer,ECRSave=Arr_Percent[|1|]] @e[type=ArmorStand,ECRStructure] setblock ~ ~-1 ~ $StructureBlock$(CALC(Arr_Percent[|1|] / 100.0))
		@e[type=ArmorStand,ECRStructure] setblock ~ ~ ~ redstone_block 0
		@e[type=ArmorStand,ECRStructure] setblock ~ ~ ~ air 0
		@a[$SA$] playsound Snd_XP voice @a[c=1] ~ ~ ~ 0.4
			
	FUNC {Func_EditCountdownTFClock}:
		@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=120] @e[type=ArmorStand,ECRRaceTimer] $Name$RaceDS = 1
		@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=120] @e[type=ArmorStand,ECRRaceTimer] $Name$RaceTI = 0
		@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=120] fill $SHead$ air 0
		@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=120] fill $BarrierFill$ air 0

	FUNC {Func_EditGeneralScoreboardEnd}:
	///	Scoreboard
		CCU.objectiveRemove(Obj_EditGeneralScoreboardStart)
		* - ECRKilled
		
	///	Other
		kill @e[type=ArmorStand,ECRAS]
		fill $SHead$ skull 4 replace {SkullType:3,Owner:{Name:"Smiley0_1",Id:"15f83218-25b3-4936-873e-20fd1aede94c"}}
		fill $BarrierFill$ barrier 0
		fill $ECRPlatform$ air 0
		
	FUNC {GLOBAL Func_AdditionalGroups}:
		GROUP {Grp_StructureBlockPulse}:
			CCU_Deactivate(GSELF)
			Func_StructureBlockPulse()
//>














