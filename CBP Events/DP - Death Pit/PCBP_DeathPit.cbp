//#-p stonebrick 0 -b -184 5 81 -print
///	C:\Users\Austin\Documents\Austin\powder game code\eGO\Events & Commands\DP - Death Pit\CBP_DeathPit.cbp

//<Defines
DEFINE $SA$ x=-188,y=24,z=78,dx=36,dy=30,dz=36
DEFINE $SPid$ 29
DEFINE $SPpvp$ 1
DEFINE $SPsat$ 0
DEFINE $Style$ 16 10 0

DEFINE $CMDName$ DP
DEFINE $CMDSizeX$ 16
DEFINE $CMDSizeY$ 14
DEFINE $CMDSizeZ$ 12

/// DEFINE $TextStart$ {"text":"","extra":[{"text":"[","color":"gold","bold":true},{"text":"DP","color":"red","bold":true},{"text":"]:","color":"gold","bold":true},{"text":" "},
DEFINE $TextStart$ {"text":"","extra":[{"text":"[","color":"gray"},{"text":"DP","color":"red","bold":"true","hoverEvent":{"action":"show_text","value":{"text":"Death Pit","color":"red"}}},{"text":"]","color":"gray"},{"text":": "},


///	SpawnMob coordinates
DEFINE $SpawnMobN$ -170 30 89
DEFINE $SpawnMobS$ -170 30 103
DEFINE $SpawnMobE$ -163 30 96
DEFINE $SpawnMobW$ -177 30 96
DEFINE $SpawnMobNE$ -165 30 91
DEFINE $SpawnMobNW$ -175 30 91
DEFINE $SpawnMobSE$ -165 30 101
DEFINE $SpawnMobSW$ -175 30 101

///	Glass Door
DEFINE $FillDoor$ -183 24 93 -183 27 99
DEFINE $SelectDoor$ x=-183,y=24,z=93,dy=3,dz=6
DEFINE $MidDoor$ -183 26.0 96

///	Select Donut
///	DPDonut
DEFINE $SelectDonut$ x=-179,y=29,z=87,dx=17,dy=4,dz=17
DEFINE $FillDonut$ -179 29 87 -161 33 105
DEFINE $DonutBlock$ -170 21 96

///	Select Spawn
DEFINE $SelectSpawn$ x=-187,y=24,z=96,r=2

///	Select Floor
DEFINE $SelectFloor$ x=-182,y=22,z=85,dx=24,dy=2,dz=24
DEFINE $FillFloor$ -182 23 84 -158 23 108
DEFINE $SelectArena$ x=-182,y=22,z=85,dx=24,dy=6,dz=24

///	Middle
DEFINE $SelectMid$ x=-170,y=29,z=96,dy=3
DEFINE $TPMiddle$ -178 24 96 -90 0
DEFINE $Middle$ -170 30 96

///	Fire line
DEFINE $FireZ$ -170 24 108 -170 24 84


DEFINE DEF_Spawn @e[type=ArmorStand,DPStand,DPmo=|0|] @e[type=AreaEffectCloud,DPDonut] summon |1| ~ ~ ~
DEFINE DEF_MidSpawn @e[type=ArmorStand,DPStand,DPmo=|0|] summon |1| PARSE($Middle$)
/// DEFINE $Spawn2$ @e[type=ArmorStand,DPStand,DPmo|0|] |1| DP + 0

DEFINE $ComplexReg$ ArmorDropChances:[0f,0f,0f,0f],HandDropChances:[0f,0f],Invulnerable:1,Silent:1,DeathLootTable:"minecraft:empty"
DEFINE $SimpleReg$ Invulnerable:1,Silent:1,DeathLootTable:"minecraft:empty"
DEFINE $NoArmor$ ArmorItems:[{},{},{},{}]
DEFINE $IronArmor$ ArmorItems:[{id:iron_boots,Count:1b},{id:iron_leggings,Count:1b},{id:iron_chestplate,Count:1b},{id:iron_helmet,Count:1b}]

DEFINE $NoHandItems$ HandItems:[{},{}]
DEFINE $HasBow$ HandItems:[{id:bow,Count:1b},{}]
DEFINE $HasPunchBow$ HandItems:[{id:bow,Count:1b,tag:{ench:[{id:49,lvl:1}]}},{}]
DEFINE $HasStoneSword$ HandItems:[{id:stone_sword,Count:1},{}]
DEFINE $HasIronSword$ HandItems:[{id:iron_sword,Count:1b},{}]

DEFINE $Invis$ {ShowParticles:0b,Duration:1000000,Id:14b,Amplifier:0b,Ambient:0b}
DEFINE $FireRes$ {ShowParticles:0b,Duration:1000000,Id:12b,Amplifier:0b,Ambient:0b}
DEFINE $Resistance$ {ShowParticles:0b,Duration:1000000,Id:11b,Amplifier:0b,Ambient:0b}
DEFINE $Regen$ {ShowParticles:0b,Duration:1000000,Id:10b,Amplifier:1b,Ambient:0b}

DEFINE $mod$ 1.1

PCBPIMPORT PCBPB_Universal.cbp
PCBPIMPORT PCBPB_PosX.cbp

FUNC Func_Stats
///	UNUSED FUNCTION due to stats not working
	@e[type=AreaEffectCloud,|0|] tellraw @e[$SA$,type=!AreaEffectCloud,|0|] baby i\'m ballin
	ScTE |1| DP 0 0
	CONDITION
		ScRE |1| DP
END

FUNC Func_Update
	@e[type=ArmorStand,DPStand] DPmo2 = 0
	@e[$SA$,type=!ArmorStand,|0|] @e[type=ArmorStand,DPStand] DPmo2 + 1
	@e[type=ArmorStand,DPStand,DPmo2>=1] ScOP |1| DP = @e[type=ArmorStand,DPStand,DPmo2>=1] DPmo2
	@e[type=ArmorStand,DPStand,DPmo2=0] ScRE |1| DP
END

FUNC Func_SpawnMobCardinal
	CONSTART
		summon AreaEffectCloud $SpawnMobN$ {Tags:["DPEntity","DPDonut"],Duration:1000000}
		summon AreaEffectCloud $SpawnMobS$ {Tags:["DPEntity","DPDonut"],Duration:1000000}
		summon AreaEffectCloud $SpawnMobE$ {Tags:["DPEntity","DPDonut"],Duration:1000000}
		summon AreaEffectCloud $SpawnMobW$ {Tags:["DPEntity","DPDonut"],Duration:1000000}
	CONEND
END

FUNC Func_SpawnMobDiagonal
	CONSTART
		summon AreaEffectCloud $SpawnMobNE$ {Tags:["DPEntity","DPDonut"],Duration:1000000}
		summon AreaEffectCloud $SpawnMobNW$ {Tags:["DPEntity","DPDonut"],Duration:1000000}
		summon AreaEffectCloud $SpawnMobSE$ {Tags:["DPEntity","DPDonut"],Duration:1000000}
		summon AreaEffectCloud $SpawnMobSW$ {Tags:["DPEntity","DPDonut"],Duration:1000000}
	CONEND
END
//>

//<Comments: To do
FUNC Func_ToDoThings
Mandatory:
	
	

Additional:
	Zombie toggle
	Display all people alive / zombie / spectate
	
Features:
	-Lingering health potions are customized to last longer
	-Resurrection zombie that matches the player's equipment when a player dies
	-Lava toggle
END
//>

//<Comments
FUNC Func_Comments
	MobID DPSave = 1
	Melee DPSave = 1
	Bow DPSave = 1
	Shield DPSave = 0
	Armor DPSave = 1
	Potion DPSave = 1
	Food DPSave = 2
	Lapis DPSave = 1
	Snowballs DPSave = 0
	Bones DPSave = 0
END
//>


//<General Scoreboard Start
FUNC Func_GeneralScoreboardStart
	USE $ObjA$
		DPSave dummy Death Pit Save
		DP dummy Death Pit
		
		DPpl dummy Players
		DPde deathCount Deaths
		DPrc deathCount Death Resurrection Calc
		DPmo dummy Death Pit Mob Calcs
		DPmo2 dummy Death Pit Mob Calcs
		DPti dummy Death Pit Timer
		DPnm dummy Death Pit Number
		DPwe dummy Death Pit Weapons
		DPwc dummy Death Pit Weapon Calc
		DPop dummy Death Pit Options
		DPdo dummy Death Pit In door
		DPcs stat.useItem.minecraft.carrot_on_a_stick Death Pit Carrot Stick
		DPhi dummy Death Pit Holding Item
		DPcd dummy Death Pit Countdown
		DPrs dummy Death Pit Regen State
		DPrt dummy Death Pit Regen Timer
		NumberObjAdd
	UNUSE
	
	USE $TeamA$
		DPp Death Pit Players
		DPd_r DP Display Red
		DPd_g DP Display Green
		DPd_y DP Display Yellow
	UNUSE
	
	USE $TeamO$
		DPd_r color red
		DPd_g color green
		DPd_y color yellow
		
		DPp color green
		DPp collisionRule never
		DPp friendlyfire false
	UNUSE

	ScTJ DPd_r Zombies Z_Jockeys Skeletons W_Skeles Spiders S_Jockeys C_Spiders CS_Jockeys Witches Wolves Blazes Guardians K_Bunnies Creepers C_Creepers C_Jockeys K_Skele
	ScTJ DPd_g Players H_Zombies
	ScTJ DPd_y /spawn
	\/spawn DP = 99
	
	ExecuteSpawnStand scoreboard objectives setdisplay sidebar DP
	ExecuteSpawnStand scoreboard objectives setdisplay belowName DPde2
///	NumberAdd(-1)
	
	$s$time set 18000
	gamerule doDaylightCycle false
	
	ExecuteSpawnStand summon ArmorStand ~ ~ ~ {Tags:["DPEntity","DPStand"],Invulnerable:1,PersistenceRequired:1,Invisible:1,Marker:1,NoGravity:1}
	DPnm + 0
	
	$s$xp -2147483648L @a[$SA$]
END
//>

//<General Scoreboard Clock
FUNC Func_GeneralScoreboardClock
///	General
	ScTJ DPp @a[$SA$,team=!DPp]
	@e[$SA$,type=Arrow] DPmo = 0 {inGround:1b}
	$s$kill @e[$SA$,type=Arrow,DPmo=0]
	
///	Upon death
	@a[DPde>=1,m=2,DPpl=1] summon AreaEffectCloud ~ ~ ~ {Tags:["DPEntity","DPResurrection"],Duration:50}
	@a[DPde>=1,m=2,DPpl>=1<=3] DPpl = 4
	@a[DPde>=1,m=2,DPpl=4,EC=0] SPbk = $SPid$
	@a[DPde>=1] DPde = 0
	
///	Upon warping back to spectate
	@a[$SelectSpawn$,m=2] DPpl + 0
	@a[$SelectSpawn$,m=2,DPpl>=0<=3] DPpl = 4
	$s$clear @a[$SelectSpawn$,m=2,DPpl=4]
	give @a[$SelectSpawn$,m=2,DPpl=4] carrot_on_a_stick 1 0 {display:{Name:"Turn into a support zombie"}}
	@a[$SelectSpawn$,m=2,DPpl=4] SPbk = $SPid$
	@a[$SA$,m=2,DPpl=4] DPpl = 5
	
///	Display
	Players DPpl = 0
	H_Zombies DPpl = 0
	@a[$SA$,m=2,DPpl>=0<=1] Players DPpl + 1
	@a[$SA$,m=2,DPpl>=2<=3] H_Zombies DPpl + 1
	ScOP Players DP = Players DPpl
	ScOP H_Zombies DP = H_Zombies DPpl
	
///	Items
	@e[$SA$,type=Item] DPmo + 0
	@e[$SA$,type=Item] DPmo2 + 0
	@e[$SA$,type=Item,DPmo=0] DPmo = 1 {Item:{id:"minecraft:skull",Damage:4s,tag:{display:{Name:"BOOMSHAKALAKA"}}}}
	@e[$SA$,type=Item,DPmo=0] DPmo = 2 {Item:{id:"minecraft:skull",Damage:1s,tag:{display:{Name:"MobBeGone Miracle Spray"}}}}
	@e[$SA$,type=Item,DPmo=0] DPmo = 10 {Item:{tag:{CanDestroy:["minecraft:bedrock"]}}}
	entitydata @e[$SA$,type=Item,DPmo>=1<=2,DPmo2=0] {Invulnerable:1,Age:-32768s,PickupDelay:32767}
	entitydata @e[$SA$,type=Item,DPmo=10,DPmo2=0] {Invulnerable:1,Age:-32768s}
	
///	Special Items
	@e[$SA$,type=Item,DPmo=0] DPmo2 = 1
	@e[$SA$,type=Item,DPmo>=1<=2] DPmo2 = 2 {OnGround:1b}
	
	@e[$SA$,type=Item,DPmo=1,DPmo2=2] summon Creeper ~ ~ ~ {Fuse:0s,ExplosionRadius:2.5,CustomName:"BOOMSHAKALAKA",CustomNameVisible:1}
	@e[$SA$,type=Item,DPmo=2,DPmo2=2] particle smoke ~ ~ ~ 1 0 1 0 200
	@e[$SA$,type=Item,DPmo=2,DPmo2=2] playsound minecraft:entity.wither.ambient voice @a
	@e[$SA$,type=Item,DPmo=2,DPmo2=2] $s$kill @e[DPMob,r=3]
	@e[$SA$,type=Item,DPmo=2,DPmo2=2] @a[m=2,r=5,DPpl>=1] DPrs = 2
	@e[$SA$,type=Item,DPmo=2,DPmo2=2] @a[m=2,r=5,DPpl>=1] DPrt = 0
	effect @a[$SA$,m=2,DPpl>=1,DPrs=2,DPrt=0] 10 5 4 true
	
///	Kills all items
	$s$kill @e[$SA$,type=Item,DPmo=0]
	$s$kill @e[$SA$,type=Item,DPmo>=1<=2,DPmo2=2]
	
///	Lingering Heath Potion	
	@e[$SA$,type=AreaEffectCloud] DPmo + 0 {Potion:"minecraft:healing"}
	entitydata @e[$SA$,type=AreaEffectCloud,DPmo=0] {Tags:["DPEntity"],Radius:3,Duration:300,RadiusPerTick:0f,RadiusOnUse:0f,ReapplicationDelay:10000,WaitTime:0}
	@e[$SA$,type=AreaEffectCloud,DPmo=0] DPmo = 1
	@e[$SA$,type=AreaEffectCloud,DPmo=1] @a[r=3,m=2,DPpl>=0,DPrs=0] DPrs = 1
	
///	Regeneration
	@a[$SA$,m=2,DPpl>=1] DPrs + 0
	@a[$SA$,m=2,DPpl>=1,DPrs>=1<=2] DPrt + 1
	effect @a[$SA$,m=2,DPpl>=1,DPrs=1,DPrt=9] 10 1 10 true
	effect @a[$SA$,m=2,DPpl>=1,DPrs=1,DPrt=10] 10 0 0 true
	@a[$SA$,m=2,DPpl>=1,DPrs=1,DPrt=10] DPrt = 0
	@a[$SA$,m=2,DPpl>=1,DPrs=1] DPrs = 0
	
	effect @a[$SA$,m=2,DPpl=2,DPrs>=1,DPrt=100] 10 0 0 true
	@a[$SA$,m=2,DPpl>=1,DPrs=2,DPrt=100] DPrs = 0
	@a[$SA$,m=2,DPpl>=1,DPrt=100] DPrt = 0
	
///	Carrot Stick	
	@a[$SA$,m=2,DPpl=5] DPhi = 1 {SelectedItem:{id:"minecraft:carrot_on_a_stick",Count:1b,tag:{display:{Name:"Turn into a support zombie"}}}}
	@a[$SA$,m=2,DPpl=5] DPhi = 1 {Inventory:[{Slot:-106b,id:"minecraft:carrot_on_a_stick",Count:1b,tag:{display:{Name:"Turn into a support zombie"}}}]}
	
	@a[$SA$,m=2,DPpl=5,DPhi=1,DPcs>=1] DPpl = 2
	
	@a[DPhi>=1] DPhi = 0
	@a[DPcs>=1] DPcs = 0
	
///	Zombie
	$s$tp @a[$SA$,m=2,DPpl=2] $TPMiddle$
	@a[$SA$,m=2,DPpl=2] DPwe = 8
	@a[DPpl=2] DPpl = 3
	
///	Getting all mob crap
	Func_Update(DPM_Z;Zombies)
	Func_Update(DPM_ZJ;Z_Jockeys)
	Func_Update(DPM_Sk;Skeletons)
	Func_Update(DPM_WS;W_Skeles)
	Func_Update(DPM_Sp;Spiders)
	Func_Update(DPM_SJ;S_Jockeys)
	Func_Update(DPM_CS;C_Spiders)
	Func_Update(DPM_CSJ;CS_Jockeys)
	Func_Update(DPM_Wi;Witches)
	Func_Update(DPM_Wo;Wolves)
	Func_Update(DPM_B;Blazes)
	Func_Update(DPM_G;Guardians)
	Func_Update(DPM_KB;K_Bunnies)
	Func_Update(DPM_C;Creepers)
	Func_Update(DPM_CC;C_Creepers)
	Func_Update(DPM_CJ;C_Jockeys)
	Func_Update(DPM_ZP;Z_Pigmen)
	Func_Update(DPM_KS;K_Skele)
END
//>

//<General Scoreboard End
FUNC Func_GeneralScoreboardEnd
	$s$clear @a[$SA$,m=2]
	$s$xp -2147483648L @a[$SA$,m=2]
	effect @a[$SA$,m=2] clear
	effect @a[$SA$,m=21] 6 1 100 true
	@a[$SA$,m=2,DPpl>=1] SPtp = $SPid$
	@a[$SA$,EC=0] SPbk = $SPid$
	
	USE $ObjR$
		DP
		DPpl
		DPde
		DPrc
		DPmo
		DPmo2
		DPti
		DPnm
		DPwe
		DPwc
		DPop
		DPdo
		DPcs
		DPhi
		DPcd
		DPrs
		DPrt
	UNUSE
	
	USE $TeamR$
		DPp
		DPd_r
		DPd_g
		DPd_y
	UNUSE
	
	$s$time set day
	$s$kill @e[$SA$,type=Item]
	$s$kill @e[$SA$,type=XPOrb]
	$s$kill @e[DPEntity]
	
	setblock $DonutBlock$ redstone_block 0
	fill $FillFloor$ obsidian 0 replace lava
END
//>


//<Open Doors Start
FUNC Func_OpenDoorsStart
	fill $FillDoor$ air 0 replace glass 0
	title @a[$SA$] title {"text":""}
	title @a[$SA$] subtitle {"text":"The door is now open","color":"green"}
END
//>

//<Open Doors Clock
FUNC Func_OpenDoorsClock
	@a[$SA$,m=2] DPdo = 0
	particle happyVillager $MidDoor$ 0.1 1.5 2 0 1 force @a
	@a[$SelectDoor$,m=2,DPdo=0] DPpl + 0
	@a[$SelectDoor$,m=2,DPdo=0,DPpl=5] DPpl = 0
	@a[$SelectDoor$,m=2,DPdo=0,DPpl=0] DPwe = 1
	@a[$SA$,m=2,DPdo=0,DPpl=0] DPpl = 1
	@a[$SelectDoor$,m=2] DPdo = 1
END
//>

//<Open Doors End
FUNC Func_OpenDoorsEnd
	fill $FillDoor$ glass 0 replace air 0
END
//>


//<General Options Testfor Clock
FUNC Func_GeneralOptionsTFClock
	testfor @e[type=ArmorStand,DPStand,DPop>=1]
	SetblockTestfor

///	Kill all mobs
	@e[type=ArmorStand,DPStand,DPop=1] $s$kill @e[$SA$,DPMob]
		
///	Lava on
	@e[type=ArmorStand,DPStand,DPop=2] fill $FillFloor$ lava 0 replace obsidian
	
///	Lava off
	@e[type=ArmorStand,DPStand,DPop=3] fill $FillFloor$ obsidian 0 replace lava
	
///	Countdown Start
	@e[type=ArmorStand,DPStand,DPop=4] summon AreaEffectCloud ~ ~ ~ {Tags:["DPEntity","DPCountdown"],Duration:250}
	testfor @e[type=ArmorStand,DPStand,DPop=4]
	CONDITION
		Activate(CBPOpenDoorsEnd)
	
///	Open doors
	testfor @e[type=ArmorStand,DPStand,DPop=5]
	CONDITION
		Activate(CBPOpenDoorsStart)
		
///	Close doors
	testfor @e[type=ArmorStand,DPStand,DPop=6]
	CONDITION
		Activate(CBPOpenDoorsEnd)
	
	USE @e[type=ArmorStand,DPStand,DPop=7]
		MobID DPSave = 1
		Melee DPSave = 1
		Bow DPSave = 0
		Shield DPSave = 0
		Armor DPSave = 1
		Potion DPSave = 1
		Food DPSave = 2
		Lapis DPSave = 1
		Snowballs DPSave = 0
		Bones DPSave = 0
	UNUSE
	
	@e[type=ArmorStand,DPStand,DPop=8] @a[$SA$,DPpl=1] DPwe = 1
	@e[type=ArmorStand,DPStand,DPop=8] @a[$SA$,DPpl=3] DPwe = 8
	
	ScOP @a[$SA$,DPpl=1] DPwe = @e[type=ArmorStand,DPStand,DPop>=9<=14] DPop
	@e[type=ArmorStand,DPStand,DPop>=9<=14] @a[$SA$,DPpl=1] DPwe - 7
	
	//-
	@e[type=ArmorStand,DPStand,DPop=15] tellraw @a[EC=0] $TextStart$ 
	{"text":"Weapons: ","color":"gray"},
	{"text":"[+]","color":"yellow",
		"hoverEvent":{"action":"show_text","value":{"text":"Melee\\n1 = Iron sword\\n2 = Iron axe\\n3 = Diamond sword\\n4 = Diamond Axe","color":"yellow"}},
		"clickEvent":{"action":"suggest_command","value":"/scoreboard players set Melee DPSave #"}
	},
	{"text":" ","color":"gray"},
	{"text":"[+]","color":"yellow",
		"hoverEvent":{"action":"show_text","value":{"text":"Bow\\n1 = 20 arrows\\n2 = Infinity","color":"yellow"}},
		"clickEvent":{"action":"suggest_command","value":"/scoreboard players set Bow DPSave #"}
	},
	{"text":" ","color":"gray"},
	{"text":"[+]","color":"yellow",
		"hoverEvent":{"action":"show_text","value":{"text":"Shield\\n1 = Shield","color":"yellow"}},
		"clickEvent":{"action":"suggest_command","value":"/scoreboard players set Shield DPSave #"}
	}]}
	
	-/-
	
	@e[type=ArmorStand,DPStand,DPop=15] tellraw @a[EC=0] $TextStart$ 
	{"text":"Armor: ","color":"gray"},
	{"text":"[+]","color":"yellow",
		"hoverEvent":{"action":"show_text","value":{"text":"Armor\\n1 = Golden\\n2 = Iron\\n3 = Diamond","color":"yellow"}},
		"clickEvent":{"action":"suggest_command","value":"/scoreboard players set Armor DPSave #"}
	}]}
	
	-/-
	@e[type=ArmorStand,DPStand,DPop=15] tellraw @a[EC=0] $TextStart$ 
	{"text":"Consumables: ","color":"gray"},
	{"text":"[+]","color":"yellow",
		"hoverEvent":{"action":"show_text","value":{"text":"Potion\\n1 = Regular\\n2 = Lingering","color":"yellow"}},
		"clickEvent":{"action":"suggest_command","value":"/scoreboard players set Potion DPSave #"}
	},
	{"text":" ","color":"gray"},
	{"text":"[+]","color":"yellow",
		"hoverEvent":{"action":"show_text","value":{"text":"Food\\n0 = Saturation\\n1 = Melons\\n2 = Bread\\n3 = Cooked Porkchop","color":"yellow"}},
		"clickEvent":{"action":"suggest_command","value":"/scoreboard players set Food DPSave #"}
	}]}
	
	-/-
	
	@e[type=ArmorStand,DPStand,DPop=15] tellraw @a[EC=0] $TextStart$ 
	{"text":"Other: ","color":"gray"},
	{"text":"[+]","color":"yellow",
		"hoverEvent":{"action":"show_text","value":{"text":"Lapis\\n1 = 64","color":"yellow"}},
		"clickEvent":{"action":"suggest_command","value":"/scoreboard players set Lapis DPSave #"}
	},
	{"text":" ","color":"gray"},
	{"text":"[+]","color":"yellow",
		"hoverEvent":{"action":"show_text","value":{"text":"Snowballs\\n1 = 16\\n2 = 64","color":"yellow"}},
		"clickEvent":{"action":"suggest_command","value":"/scoreboard players set Snowballs DPSave #"}
	},
	{"text":" ","color":"gray"},
	{"text":"[+]","color":"yellow",
		"hoverEvent":{"action":"show_text","value":{"text":"Bones\\n1 = 16\\n2 = 64","color":"yellow"}},
		"clickEvent":{"action":"suggest_command","value":"/scoreboard players set Bones DPSave #"}
	}]}
	
	\\-
	
	@e[type=ArmorStand,DPStand,DPop>=1] DPop = 0
END
//>

//<Summon Mob Testfor Numbers
FUNC Func_SummonMobTFClock
	testfor @e[type=ArmorStand,DPStand,DPnm>=1]
	SetblockTestfor
	
///	Initializes mob spawning
	@e[type=ArmorStand,DPStand,DPnm>=1] DPmo = 1
	
	@e[type=ArmorStand,DPStand,DPnm>=1] $s$kill @e[$SA$,type=AreaEffectCloud,DPDonut]
///	Summons any number between 4 to 24, multiples of 4
	LOOP 24 4 - 4 (Func_SpawnMobDiagonal;Func_SpawnMobCardinal;Func_SpawnMobDiagonal;Func_SpawnMobCardinal;Func_SpawnMobDiagonal;Func_SpawnMobCardinal)
		testfor @e[type=ArmorStand,DPStand,DPnm>=|0|]
			|1|
		@e[type=ArmorStand,DPStand,DPnm>=|0|] DPnm - 4
	END

///	Reset Number
	@e[type=ArmorStand,DPStand,DPnm>=1] DPnm = 0
END
//>

//<Countdown Testfor Clock
FUNC Func_CountdownTFClock
	testfor @e[type=AreaEffectCloud,DPCountdown]
	SetblockTestfor
	
	@e[type=AreaEffectCloud,DPCountdown] DPti + 1
	
	@e[type=AreaEffectCloud,DPCountdown,DPti=20] tellraw @a $TextStart${"text":"5!","color":"yellow"}]}
	@e[type=AreaEffectCloud,DPCountdown,DPti=20] @a playsound minecraft:block.note.pling voice @a[c=1] ~ ~ ~ 0.5
	
	@e[type=AreaEffectCloud,DPCountdown,DPti=40] tellraw @a $TextStart${"text":"4!","color":"yellow"}]}
	@e[type=AreaEffectCloud,DPCountdown,DPti=40] @a playsound minecraft:block.note.pling voice @a[c=1] ~ ~ ~ 1
	
	@e[type=AreaEffectCloud,DPCountdown,DPti=60] tellraw @a $TextStart${"text":"3!","color":"yellow"}]}
	@e[type=AreaEffectCloud,DPCountdown,DPti=60] @a playsound minecraft:block.note.pling voice @a[c=1] ~ ~ ~ 1.5
	
	@e[type=AreaEffectCloud,DPCountdown,DPti=80] tellraw @a $TextStart${"text":"2!","color":"yellow"}]}
	@e[type=AreaEffectCloud,DPCountdown,DPti=80] @a playsound minecraft:block.note.pling voice @a[c=1] ~ ~ ~ 2
	
	@e[type=AreaEffectCloud,DPCountdown,DPti=100] tellraw @a $TextStart${"text":"1!","color":"yellow"}]}
	@e[type=AreaEffectCloud,DPCountdown,DPti=100] @a playsound minecraft:block.note.pling voice @a[c=1] ~ ~ ~ 2
	
	@e[type=AreaEffectCloud,DPCountdown,DPti=120] tellraw @a $TextStart${"text":"Begin!","color":"yellow"}]}
	@e[type=AreaEffectCloud,DPCountdown,DPti=120] @a playsound entity.experience_orb.touch voice @a[c=1]
	@e[type=AreaEffectCloud,DPCountdown,DPti=120] fill $FillDonut$ air 0
	@e[type=AreaEffectCloud,DPCountdown,DPti=123] @a playsound entity.experience_orb.touch voice @a[c=1]
	@e[type=AreaEffectCloud,DPCountdown,DPti=126] @a playsound entity.experience_orb.touch voice @a[c=1]
	
	@e[type=AreaEffectCloud,DPCountdown,DPti>=120] @e[$SA$,DPMob] DPcd + 0
	@e[type=AreaEffectCloud,DPCountdown,DPti>=120] entitydata @e[$SA$,DPMob,DPcd=0] {Silent:0,Invulnerable:0}
	@e[type=AreaEffectCloud,DPCountdown,DPti>=120] effect @e[$SA$,DPMob,DPcd=0] 8 3 255 true
	@e[$SA$,DPMob,DPcd=0] DPcd = 1
	
	@e[type=AreaEffectCloud,DPCountdown,DPti=220] setblock $DonutBlock$ redstone_block 0
END
//>

//<Spawn Mob Testfor Clock
FUNC Func_SpawnMobTFClock
	testfor @e[type=ArmorStand,DPStand,DPmo>=1]
	SetblockTestfor
	
	ScOP @e[type=ArmorStand,DPStand,DPmo=1] DPmo = MobID DPSave
///	Zombie
	DEF_Spawn(1;Zombie) {Tags:["DPEntity","DPMob","DPM_Z"],$NoArmor$,$NoHandItems$,$ComplexReg$}
	DEF_Spawn(2;Zombie) {Tags:["DPEntity","DPMob","DPM_Z"],$NoArmor$,$HasIronSword$,$ComplexReg$}
	DEF_Spawn(3;Zombie) {Tags:["DPEntity","DPMob","DPM_Z"],$IronArmor$,$NoHandItems$,$ComplexReg$}
	DEF_Spawn(4;Zombie) {Tags:["DPEntity","DPMob","DPM_Z"],$IronArmor$,$HasIronSword$,$ComplexReg$}
	
	DEF_Spawn(5;Zombie) {Tags:["DPEntity","DPMob","DPM_Z"],IsBaby:1,$NoArmor$,$NoHandItems$,$ComplexReg$}
	DEF_Spawn(6;Zombie) {Tags:["DPEntity","DPMob","DPM_Z"],ZombieType:6,$NoArmor$,$NoHandItems$,$ComplexReg$}
	
	
///	Zombie Jockey
	//-
	DEF_Spawn(10;Chicken) {Tags:["DPEntity","DPMob"],$SimpleReg$,Passengers:[
		{id:Zombie,Tags:["DPEntity","DPMob","DPM_ZJ"],IsBaby:1,$NoArmor$,$NoHandItems$,$ComplexReg$}]}
	
	-/-
///	Mini Jade
	DEF_Spawn(11;Zombie) {Tags:["DPEntity","DPMob","DPM_Z","DPJade"],IsBaby:1,CustomName:"Mini Jade",
	ArmorItems:[{id:leather_boots,Count:1b,tag:{display:{color:16777215}}},
		{id:leather_leggings,Count:1b,tag:{display:{color:16777215}}},
		{id:leather_chestplate,Count:1b,tag:{display:{color:15892389}}},
		{id:skull,Count:1b,Damage:3s,tag:{SkullOwner:Jadeofalltrades}}],
	$NoHandItems$,$ComplexReg$}
	
	-/-
///	Mini Jade Jockey
	DEF_Spawn(12;Chicken) {Tags:["DPEntity","DPMob"],$SimpleReg$,Passengers:[
		{id:Zombie,Tags:["DPEntity","DPMob","DPM_ZJ","DPJade"],IsBaby:1,CustomName:"Mini Jade",
		ArmorItems:[{id:leather_boots,Count:1b,tag:{display:{color:16777215}}},
			{id:leather_leggings,Count:1b,tag:{display:{color:16777215}}},
			{id:leather_chestplate,Count:1b,tag:{display:{color:15892389}}},
			{id:skull,Count:1b,Damage:3s,tag:{SkullOwner:Jadeofalltrades}}],
		$NoHandItems$,$ComplexReg$}]}
	
	-/-
///	Creeper Zombie
	DEF_Spawn(20;Zombie) {Tags:["DPEntity","DPMob","DPM_Z"],
	ArmorItems:[{id:chainmail_boots,Count:1b},
		{id:chainmail_leggings,Count:1b},
		{id:chainmail_chestplate,Count:1b},
		{id:skull,Count:1b,Damage:4s,tag:{display:{Name:"BOOMSHAKALAKA"},CanDestroy:["minecraft:bedrock"]}}],
	HandItems:[{id:golden_sword,Count:1b,tag:{Unbreakable:1,ench:[{id:19,lvl:2}]}},{}],
	ArmorDropChances:[0f,0f,0f,0.75f],HandDropChances:[0.5f,0f],$SimpleReg$}
	
	-/-
///	Smelly Savvy
	DEF_Spawn(21;Zombie) {Tags:["DPEntity","DPMob","DPM_Z","DPSavvy"],CustomName:"Smelly Savvy",
	ArmorItems:[{id:leather_boots,Count:1b},
		{id:leather_leggings,Count:1b,tag:{display:{color:6704179}}},
		{id:leather_chestplate,Count:1b,tag:{display:{color:6717235}}},
		{id:skull,Count:1b,Damage:3s,tag:{SkullOwner:"savvykms"}}]
	,$NoHandItems$,$ComplexReg$}
	
	-/-
///	Chars (Fire Zombie)
	DEF_Spawn(22;Zombie) {Tags:["DPEntity","DPMob","DPM_Z","DPFire"],CustomName:"Chars",Fire:32767s,
	Attributes:[{Name:"generic.movementSpeed",Base:0.35}],
	ActiveEffects:[$FireRes$,$Invis$],
	ArmorItems:[{},{},{},{id:skull,Count:1b,Damage:2s}],
	$NoHandItems$,$ComplexReg$}
	\\-
	
	//(Rebalance chars to be slow but tanky and hits hard)
	
///	Skeleton
	DEF_Spawn(50;Skeleton) {Tags:["DPEntity","DPMob","DPEntity","DPM_Sk"],$NoArmor$,$HasBow$,$ComplexReg$}
	DEF_Spawn(51;Skeleton) {Tags:["DPEntity","DPMob","DPM_Sk"],$NoArmor$,$HasPunchBow$,$ComplexReg$}
	DEF_Spawn(52;Skeleton) {Tags:["DPEntity","DPMob","DPM_Sk"],$IronArmor$,$HasBow$,$ComplexReg$}
	DEF_Spawn(53;Skeleton) {Tags:["DPEntity","DPMob","DPM_Sk"],$IronArmor$,$HasPunchBow$,$ComplexReg$}
	DEF_Spawn(54;Skeleton) {Tags:["DPEntity","DPMob","DPM_Sk"],SkeletonType:2,$NoArmor$,$HasBow$,$ComplexReg$}
	
	DEF_Spawn(60;Skeleton) {Tags:["DPEntity","DPMob","DPM_WS"],SkeletonType:1,$NoArmor$,$HasStoneSword$,$ComplexReg$}
	
///	Wither skele with iron gear
	//-
	DEF_Spawn(61;Skeleton) {Tags:["DPEntity","DPMob","DPM_WS"],SkeletonType:1,
	ArmorItems:[{id:iron_boots,Count:1b},
		{id:iron_leggings,Count:1b},
		{id:iron_chestplate,Count:1b},
		{id:skull,Count:1b,Damage:1s,tag:{display:{Name:"MobBeGone Miracle Spray"}}}],
	$HasIronSword$,ArmorDropChances:[0f,0f,0f,0.35f],HandDropChances:[0f,0f],$SimpleReg$}
	
	-/-
///	Boss Skeleton
	DEF_MidSpawn(70;Chicken) 
	{Tags:["DPEntity","DPMob","DPChicken"],ActiveEffects:[$FireRes$,$Invis$],$SimpleReg$,Passengers:[
	{id:Zombie,IsBaby:1,Tags:["DPEntity","DPMob","DPS_Z"],ActiveEffects:[$FireRes$],ArmorItems:[{},{},{},{id:skull,Count:1b}],$NoHandItems$,$ComplexReg$},
	{id:Zombie,IsBaby:1,Tags:["DPEntity","DPMob","DPS_Z"],ActiveEffects:[$FireRes$],ArmorItems:[{},{},{},{id:skull,Count:1b}],$NoHandItems$,$ComplexReg$},
	{id:Zombie,IsBaby:1,Tags:["DPEntity","DPMob","DPS_Z"],ActiveEffects:[$FireRes$],ArmorItems:[{},{},{},{id:skull,Count:1b}],$NoHandItems$,$ComplexReg$},
	{id:Zombie,IsBaby:1,Tags:["DPEntity","DPMob","DPS_Z"],ActiveEffects:[$FireRes$],ArmorItems:[{},{},{},{id:skull,Count:1b}],$NoHandItems$,$ComplexReg$},
	{id:Guardian,Tags:["DPEntity","DPMob","DPS_G"],ActiveEffects:[$FireRes$],$SimpleReg$,Passengers:[
		{id:Skeleton,Tags:["DPEntity","DPMob","DPS_KS"],ActiveEffects:[$FireRes$],CustomName:"King Skeleton",CustomNameVisible:1,$ComplexReg$,
			ArmorItems:[{id:diamond_boots,Count:1b},
			{id:diamond_leggings,Count:1b},
			{id:diamond_chestplate,Count:1b},
			{id:diamond_helmet,Count:1b}],
		HandItems:[{id:bow,Count:1b,tag:{ench:[{id:48,lvl:2}]}},{}]
		}]
	}]}
	\\-
	
///	Spiders
	DEF_Spawn(100;Spider) {Tags:["DPEntity","DPMob","DPM_Sp"],$SimpleReg$}
	DEF_Spawn(101;Spider) {Tags:["DPEntity","DPMob","DPM_Sp"],ActiveEffects:[$Invis$],$SimpleReg$}
	
///	Regular spider jockey
	//-
	DEF_Spawn(102;Spider) {Tags:["DPEntity","DPMob","DPM_SJ"],$SimpleReg$,Passengers:[
		{id:Skeleton,Tags:["DPEntity","DPMob","DPM_SJ"],$NoArmor$,$HasBow$,$ComplexReg$}]}
	
	-/-
///	Invis spider jockey
	DEF_Spawn(103;Spider) {Tags:["DPEntity","DPMob","DPM_SJ"],$SimpleReg$,ActiveEffects:[$Invis$],Passengers:[
		{id:Skeleton,Tags:["DPEntity","DPMob","DPM_SJ"],$NoArmor$,$HasBow$,$ComplexReg$}]}
	
	-/-
///	FlameVenom (Fire Spider)
	DEF_Spawn(104;Spider) {Tags:["DPEntity","DPMob","DPM_Sp","DPFire"],CustomName:"FlameVenom",Fire:32767s,
		HandItems:[{id:blaze_rod,Count:1b,tag:{ench:[{id:20,lvl:2}]}},{}],
		ActiveEffects:[$FireRes$],$ComplexReg$}
	\\-
	//(Change spidervenom to have a longer hitting fire attack, but deals minimal damage)
	
///	Cave Spiders
	DEF_Spawn(110;CaveSpider) {Tags:["DPEntity","DPMob","DPM_CS"],$SimpleReg$}
	DEF_Spawn(111;CaveSpider) {Tags:["DPEntity","DPMob","DPM_CS"],ActiveEffects:[$Invis$],$SimpleReg$}
	
///	Cave spider jockey
	//-
	DEF_Spawn(112;CaveSpider) {Tags:["DPEntity","DPMob","DPM_CSJ"],$SimpleReg$,Passengers:[
		{id:Skeleton,Tags:["DPEntity","DPMob","DPM_CSJ"],$NoArmor$,$HasBow$,$ComplexReg$}]}
	
	-/-
///	Invis cave spider jockey
	DEF_Spawn(113;CaveSpider) {Tags:["DPEntity","DPMob","DPM_CSJ"],ActiveEffects:[$Invis$],$SimpleReg$,Passengers:[
		{id:Skeleton,Tags:["DPEntity","DPMob","DPM_CSJ"],$NoArmor$,$HasBow$,$ComplexReg$}]}
	
	-/-
///	Creeper Jockey
	DEF_Spawn(114;CaveSpider) {Tags:["DPEntity","DPMob","DPM_CJ"],$SimpleReg$,Passengers:[{id:Creeper,Tags:["DPEntity","DPMob","DPM_CJ"],$SimpleReg$}]}
	\\-
	
///	Witch
	DEF_Spawn(150;Witch) {Tags:["DPEntity","DPMob","DPM_Wi"],$SimpleReg$}
	DEF_Spawn(151;Witch) {Tags:["DPEntity","DPMob","DPM_Wi"],ActiveEffects:[$Resistance$],$SimpleReg$}
///	DEF_Spawn(152;Witch) //(Fire resistance potion drop, resistance 2)
	
///	Wolf
	DEF_Spawn(200;Wolf) {Tags:["DPEntity","DPMob","DPM_Wo"],$SimpleReg$}
	DEF_Spawn(201;Wolf) {Tags:["DPEntity","DPMob","DPM_Wo"],ActiveEffects:[$FireRes$],$SimpleReg$}
	DEF_Spawn(202;Wolf) {Tags:["DPEntity","DPMob","DPM_Wo"],ActiveEffects:[$Regen$],$SimpleReg$}
	DEF_Spawn(203;Wolf) {Tags:["DPEntity","DPMob","DPM_Wo"],ActiveEffects:[$FireRes$,$FireRes$],$SimpleReg$}
	
///	Blaze
	DEF_Spawn(250;Blaze) {Tags:["DPEntity","DPMob","DPM_B"],$SimpleReg$}
	
///	Blaze Turret
	//-	
	DEF_MidSpawn(251;Blaze) {Tags:["DPEntity","DPMob","DPM_BT"],CustomName:"Blaze Turret",Health:150f,Fire:32767s,
		Attributes:[{Name:"generic.maxHealth",Base:150d},
		{Name:"generic.knockbackResistance",Base:1}],
	NoAI:1,$ComplexReg$}
	\\-
///	Guardian
	DEF_Spawn(300;Guardian) {Tags:["DPEntity","DPMob","DPM_G"],$SimpleReg$}
	DEF_Spawn(301;Guardian) {Tags:["DPEntity","DPMob","DPM_G"],ActiveEffects:[$FireRes$],$SimpleReg$}
	
///	Killer Bunny
	DEF_Spawn(350;Rabbit) {Tags:["DPEntity","DPMob","DPM_KB"],RabbitType:99,CustomName:"A Harmless Bunny",$SimpleReg$}
	
///	Creeper
	DEF_Spawn(400;Creeper) {Tags:["DPEntity","DPMob","DPM_C"],$SimpleReg$}
	//(Charged Creeper)
///	summon Creeper -170 30 96 {Tags:["DPM_CC"],powered:1,ActiveEffects:[{ShowParticles:0b,Duration:1000000,Id:12b,Amplifier:0b,Ambient:0b}],Silent:1,DeathLootTable:"minecraft:empty",$SimpleReg$}
	//(Fire Explosion)
///	{CustomName:"Fireball",Fire:32767s,ActiveEffects:[{ShowParticles:0b,Duration:1000000,Id:12b,Amplifier:0b,Ambient:0b}],Tags:["DPM_C","DPMob","DPFire"],Silent:1,DeathLootTable:"minecraft:empty"}
///	Have it so an invisible armor stand is above it, and once it lands, summons a fireball
	
///	Zombie Pigman
	DEF_Spawn(450;PigZombie) {Tags:["DPEntity","DPMob","DPM_ZP"],$ComplexReg$}
	
///	Troll Pigman
	//-
	DEF_Spawn(451;PigZombie) {Tags:["DPEntity","DPMob","DPM_ZP"],CustomName:"LOLOLOL",
		Attributes:[{Name:"generic.attackDamage",Base:0},
		{Name:"generic.movementSpeed",Base:0.35}],
	$NoArmor$,HandItems:[{id:sign,Count:1b,tag:{ench:[{id:19,lvl:2}]}},{}],$ComplexReg$}
	
	-/-
///	Zisteau
	DEF_Spawn(452;PigZombie) {Tags:["DPEntity","DPMob","DPM_ZP"],CustomName:"Zisteau",ArmorItems:[{},{id:leather_leggings,Count:1b,tag:{ench:[{id:0,lvl:3}]}},{},{}],HandItems:[{id:lava_bucket,Count:1b,tag:{ench:[{id:20,lvl:1},{id:16,lvl:1}]}},{}],$ComplexReg$}
	\\-
	
	
//(Polar bears)
//(Man Bear Pig)
//(Baby Polar bear at mid, killed when all polar bears are dead)
	@e[type=ArmorStand,DPStand,DPmo>=1] $s$kill @e[$SA$,type=AreaEffectCloud,DPDonut]
	@e[type=ArmorStand,DPStand,DPmo>=1] DPmo = 0
END
//>

//<Equpiment Testfor Clock
FUNC Func_EqupimentTFClock
	testfor @a[DPwe>=1<=8]
	SetblockTestfor
///	DPwe
///	1 = all, 2 = equipment, 3 = refresh potions only, 4 = refresh food only, 5 = lapis, 6 = snowballs, 7 = bones, 8 = zambie
///	DPpl
///	0 = new, 1 = Normal, 2 = Zombie start, 3 = Zombie Fin, 4 = Dead
	
	$s$clear @a[$SA$,m=2,DPwe=1]
	
	LOOP (golden;iron;diamond)
		$s$clear @a[$SA$,m=2,DPwe=2] |0|_helmet
		$s$clear @a[$SA$,m=2,DPwe=2] |0|_chestplate
		$s$clear @a[$SA$,m=2,DPwe=2] |0|_leggings
		$s$clear @a[$SA$,m=2,DPwe=2] |0|_boots
	END
	$s$clear @a[$SA$,m=2,DPwe=2] iron_sword
	$s$clear @a[$SA$,m=2,DPwe=2] iron_axe
	$s$clear @a[$SA$,m=2,DPwe=2] diamond_sword
	$s$clear @a[$SA$,m=2,DPwe=2] diamond_axe
	$s$clear @a[$SA$,m=2,DPwe=2] bow
	$s$clear @a[$SA$,m=2,DPwe=2] arrow
	$s$clear @a[$SA$,m=2,DPwe=2] shield
	
	$s$clear @a[$SA$,m=2,DPwe=3] potion
	$s$clear @a[$SA$,m=2,DPwe=3] lingering_potion
	$s$clear @a[$SA$,m=2,DPwe=4] melon
	$s$clear @a[$SA$,m=2,DPwe=4] bread
	$s$clear @a[$SA$,m=2,DPwe=4] cooked_porkchop
	$s$clear @a[$SA$,m=2,DPwe=5] dye
	$s$clear @a[$SA$,m=2,DPwe=6] snowball
	$s$clear @a[$SA$,m=2,DPwe=7] bones
	$s$clear @a[$SA$,m=2,DPwe=8]
	
	effect @a[$SA$,m=2,DPwe>=1<=8] clear
	$s$xp -2147483648L @a[$SA$,m=2,DPwe=1]
	$s$xp -2147483648L @a[$SA$,m=2,DPwe=8]
	effect @a[$SA$,m=2,DPwe=1] 6 1 100 true
	effect @a[$SA$,m=2,DPwe=8] 6 1 100 true
	
///	Melee
///	1 = iron sword, 2 = iron axe
///	3 = dia sword, 4 = dia axe
	ScOP @e[type=ArmorStand,DPStand] DPwe = Melee DPSave
	@a[$SA$,m=2,DPwe>=2] DPwc = 0
	@a[$SA$,m=2,DPwe=1] DPwc = 1
	@a[$SA$,m=2,DPwe=2] DPwc = 1
	
	@e[type=ArmorStand,DPStand,DPwe=1] replaceitem entity @a[$SA$,m=2,DPwc=1] slot.hotbar.0 iron_sword 1 0 {Unbreakable:1,HideFlags:8}
	@e[type=ArmorStand,DPStand,DPwe=2] replaceitem entity @a[$SA$,m=2,DPwc=1] slot.hotbar.0 iron_axe 1 0 {Unbreakable:1,HideFlags:8}
	@e[type=ArmorStand,DPStand,DPwe=3] replaceitem entity @a[$SA$,m=2,DPwc=1] slot.hotbar.0 diamond_sword 1 0 {Unbreakable:1,HideFlags:8}
	@e[type=ArmorStand,DPStand,DPwe=4] replaceitem entity @a[$SA$,m=2,DPwc=1] slot.hotbar.0 diamond_axe 1 0 {Unbreakable:1,HideFlags:8}
	
///	Bow
///	1 = regular with 20 arrows
///	2 = infinity
	ScOP @e[type=ArmorStand,DPStand] DPwe = Bow DPSave
	
	@e[type=ArmorStand,DPStand,DPwe=1] $s$give @a[$SA$,m=2,DPwc=1] bow 1 0 {Unbreakable:1,HideFlags:8}
	@e[type=ArmorStand,DPStand,DPwe=1] $s$give @a[$SA$,m=2,DPwc=1] arrow 20 0 {HideFlags:8}
	@e[type=ArmorStand,DPStand,DPwe=2] $s$give @a[$SA$,m=2,DPwc=1] bow 1 0 {ench:[{id:51,lvl:1}],Unbreakable:1,HideFlags:8}
	@e[type=ArmorStand,DPStand,DPwe=2] replaceitem entity @a[$SA$,m=2,DPwc=1] slot.inventory.8 arrow 1 0 {HideFlags:8}
	
///	Shield
	ScOP @e[type=ArmorStand,DPStand] DPwe = Shield DPSave
	@e[type=ArmorStand,DPStand,DPwe=1] replaceitem entity @a[$SA$,m=2,DPwe=1] slot.weapon.offhand shield 1 0 {Unbreakable:1,HideFlags:8}
	
///	Armor
///	1 = gold, 2 = iron, 3 = dia
	ScOP @e[type=ArmorStand,DPStand] DPwe = Armor DPSave
	
	LOOP 1 3 + 1 (golden;iron;diamond)
		@e[type=ArmorStand,DPStand,DPwe=|0|] replaceitem entity @a[$SA$,m=2,DPwc=1] slot.armor.head |1|_helmet 1 0 {Unbreakable:1,HideFlags:8}
		@e[type=ArmorStand,DPStand,DPwe=|0|] replaceitem entity @a[$SA$,m=2,DPwc=1] slot.armor.chest |1|_chestplate 1 0 {Unbreakable:1,HideFlags:8}
		@e[type=ArmorStand,DPStand,DPwe=|0|] replaceitem entity @a[$SA$,m=2,DPwc=1] slot.armor.legs |1|_leggings 1 0 {Unbreakable:1,HideFlags:8}
		@e[type=ArmorStand,DPStand,DPwe=|0|] replaceitem entity @a[$SA$,m=2,DPwc=1] slot.armor.feet |1|_boots 1 0 {Unbreakable:1,HideFlags:8}
	END
	
///	Potions
///	1 = Instant Health, 2 = Lingering Instant Health (II)
	ScOP @e[type=ArmorStand,DPStand] DPwe = Potion DPSave
	@a[$SA$,m=2,DPwe>=2] DPwc = 0
	@a[$SA$,m=2,DPwe=1] DPwc = 1
	@a[$SA$,m=2,DPwe=3] DPwc = 1
	
	@e[type=ArmorStand,DPStand,DPwe=1] $s$give @a[$SA$,m=2,DPwc=1] potion 4 0 {Potion:"healing",HideFlags:8}
	@e[type=ArmorStand,DPStand,DPwe=2] $s$give @a[$SA$,m=2,DPwc=1] lingering_potion 2 0 {Potion:"healing",HideFlags:8}
	
///	Food
///	0 = None, 1 = Melons, 2 = Bread, 3 = Cooked porkchop
	ScOP @e[type=ArmorStand,DPStand] DPwe = Food DPSave
	@a[$SA$,m=2,DPwe>=2] DPwc = 0
	@a[$SA$,m=2,DPwe=1] DPwc = 1
	@a[$SA$,m=2,DPwe=4] DPwc = 1
	
	@e[type=ArmorStand,DPStand,DPwe=0] @e[type=ArmorStand,SpawnStand] SPsat = 1
	@e[type=ArmorStand,DPStand,DPwe>=1<=3] @e[type=ArmorStand,SpawnStand] SPsat = 0
	@e[type=ArmorStand,DPStand,DPwe=1] $s$give @a[$SA$,m=2,DPwc=1] melon 64 0 {HideFlags:8}
	@e[type=ArmorStand,DPStand,DPwe=2] $s$give @a[$SA$,m=2,DPwc=1] bread 64 0 {HideFlags:8}
	@e[type=ArmorStand,DPStand,DPwe=3] $s$give @a[$SA$,m=2,DPwc=1] cooked_porkchop 64 0 {HideFlags:8}
	
///	Lapis
///	0 = None, 1 = 64
	ScOP @e[type=ArmorStand,DPStand] DPwe = Lapis DPSave
	@a[$SA$,m=2,DPwe>=2] DPwc = 0
	@a[$SA$,m=2,DPwe=1] DPwc = 1
	@a[$SA$,m=2,DPwe=5] DPwc = 1
	
	@e[type=ArmorStand,DPStand,DPwe=1] $s$give @a[$SA$,m=2,DPwc=1] dye 64 4 {HideFlags:8}
	
///	Snowballs
///	0 = None, 1 = 16, 2 = 64
	ScOP @e[type=ArmorStand,DPStand] DPwe = Snowballs DPSave
	@a[$SA$,m=2,DPwe>=2] DPwc = 0
	@a[$SA$,m=2,DPwe=1] DPwc = 1
	@a[$SA$,m=2,DPwe=6] DPwc = 1
	
	@e[type=ArmorStand,DPStand,DPwe=1] $s$give @a[$SA$,m=2,DPwc=1] snowball 16 0 {HideFlags:8}
	@e[type=ArmorStand,DPStand,DPwe=2] $s$give @a[$SA$,m=2,DPwc=1] snowball 64 0 {HideFlags:8}
	
///	Bones
///	0 = None, 1 = 16, 2 = 64
	ScOP @e[type=ArmorStand,DPStand] DPwe = Bones DPSave
	@a[$SA$,m=2,DPwe>=2] DPwc = 0
	@a[$SA$,m=2,DPwe=1] DPwc = 1
	@a[$SA$,m=2,DPwe=7] DPwc = 1
	
	@e[type=ArmorStand,DPStand,DPwe=1] $s$give @a[$SA$,m=2,DPwc=1] bone 16 0 {HideFlags:8}
	@e[type=ArmorStand,DPStand,DPwe=2] $s$give @a[$SA$,m=2,DPwc=1] bone 64 0 {HideFlags:8}
	
///	Zombies
///	Literally no options lol
	@a[$SA$,m=2,DPwe>=1] DPwc = 0
	@a[$SA$,m=2,DPwe=8] DPwc = 1
	
	replaceitem entity @a[$SA$,m=2,DPwc=1] slot.weapon wooden_sword 1 0 {Unbreakable:1}
	replaceitem entity @a[$SA$,m=2,DPwc=1] slot.armor.head skull 1 2
	replaceitem entity @a[$SA$,m=2,DPwc=1] slot.armor.chest leather_chestplate 1 0 {Unbreakable:1,display:{color:38807}}
	replaceitem entity @a[$SA$,m=2,DPwc=1] slot.armor.legs leather_leggings 1 0 {Unbreakable:1,display:{color:5589194}}
	replaceitem entity @a[$SA$,m=2,DPwc=1] slot.armor.feet leather_boots 1 0 {Unbreakable:1,display:{color:2500134}}
	replaceitem entity @a[$SA$,m=2,DPwc=1] slot.weapon.offhand rotten_flesh 64 0 {display:{Name:"Beef Jerky"}}
	
	@a[$SA$,m=2,DPwe>=1] SPbk = $SPid$
	
	@e[type=ArmorStand,DPStand] DPwe = 0
	@a[DPwe>=1] DPwe = 0
	@a[DPwc>=1] DPwc = 0
END
//>


//<Mob Testfor Clock: Resurrection
FUNC Func_MobTFClock_Resurrection
	testfor @e[type=AreaEffectCloud,DPResurrection]
	SetblockTestfor
	
	//-
	@e[type=AreaEffectCloud,DPResurrection] summon Zombie ~ ~ ~ {Tags:["DPEntity","DPMob","DPM_Z","DPResurrection"],
	CustomName:"The Resurrection",CustomNameVisible:1,
	ActiveEffects:[$FireRes$],
	Attributes:[{Name:"generic.movementSpeed",Base:0.35},
	$NoArmor$,$NoHandItems$,$ComplexReg$}
	\\-
	
	@e[type=AreaEffectCloud,DPResurrection] playsound minecraft:entity.wither.spawn voice @a ~ ~ ~ 0.5
	@e[type=AreaEffectCloud,DPResurrection] Zombies DP 1
	@e[type=Zombie,DPResurrection] DPmo + 0
	
///	Custom items are set on the zombie
	ScOP @e[type=ArmorStand,DPStand] DPwe = Armor DPSave
	LOOP 1 3 + 1 (golden;iron;diamond)
		@e[type=ArmorStand,DPStand,DPwe=|0|] replaceitem entity @e[type=Zombie,DPResurrection,DPmo=0] slot.armor.chest |1|_chestplate 1 0 {Unbreakable:1,HideFlags:8}
		@e[type=ArmorStand,DPStand,DPwe=|0|] replaceitem entity @e[type=Zombie,DPResurrection,DPmo=0] slot.armor.legs |1|_leggings 1 0 {Unbreakable:1,HideFlags:8}
		@e[type=ArmorStand,DPStand,DPwe=|0|] replaceitem entity @e[type=Zombie,DPResurrection,DPmo=0] slot.armor.feet |1|_boots 1 0 {Unbreakable:1,HideFlags:8}
	END
	
	ScOP @e[type=ArmorStand,DPStand] DPwe = Melee DPSave
	@e[type=ArmorStand,DPStand,DPwe=1] replaceitem entity @e[type=Zombie,DPResurrection,DPmo=0] slot.weapon iron_sword 1 0
	@e[type=ArmorStand,DPStand,DPwe=2] replaceitem entity @e[type=Zombie,DPResurrection,DPmo=0] slot.weapon iron_axe 1 0
	@e[type=ArmorStand,DPStand,DPwe=3] replaceitem entity @e[type=Zombie,DPResurrection,DPmo=0] slot.weapon diamond_sword 1 0
	@e[type=ArmorStand,DPStand,DPwe=4] replaceitem entity @e[type=Zombie,DPResurrection,DPmo=0] slot.weapon diamond_axe 1 0
	
	ScOP @e[type=ArmorStand,DPStand] DPwe = Food DPSave
	@e[type=ArmorStand,DPStand,DPwe=1] replaceitem entity @e[type=Zombie,DPResurrection,DPmo=0] slot.weapon.offhand melon 1 0
	@e[type=ArmorStand,DPStand,DPwe=2] replaceitem entity @e[type=Zombie,DPResurrection,DPmo=0] slot.weapon.offhand bread 1 0
	@e[type=ArmorStand,DPStand,DPwe=3] replaceitem entity @e[type=Zombie,DPResurrection,DPmo=0] slot.weapon.offhand cooked_porkchop 1 0
	
	ScOP @e[type=ArmorStand,DPStand] DPwe = Shield DPSave
	@e[type=ArmorStand,DPStand,DPwe=1] replaceitem entity @e[type=Zombie,DPResurrection,DPmo=0] slot.weapon.offhand shield 1 0
	
	@e[type=ArmorStand,DPStand] DPwe = 0
	@e[type=Zombie,DPResurrection,DPmo=0] DPmo = 1
	
	$s$kill @e[type=AreaEffectCloud,DPResurrection]
END
//>

//<Mob Testfor Clock: Jade
FUNC Func_MobTFClock_Jade
	testfor @e[type=Zombie,DPJade]
	SetblockTestfor
	
	@r[$SA$,type=Zombie,DPJade] DPmo + 0
	@e[$SA$,type=Zombie,DPJade,DPmo>=0<=9] DPmo + 1
	@e[$SelectArena$,type=Zombie,DPmo=10] particle heart ~ ~1.5 ~ 0 0 0 0 1 force @a
	@e[$SA$,type=Zombie,DPJade,DPmo=10] DPmo = 0
END
//>

//<Mob Testfor Clock: Smelly Savvy
FUNC Func_MobTFClock_SmellySavvy
	testfor @e[$SelectArena$,type=Zombie,DPSavvy]
	SetblockTestfor
	
	@e[$SelectArena$,type=Zombie,DPSavvy] effect @a[m=2,r=2,DPpl>=1<=8] 19 5 0 true
	@e[$SelectArena$,type=Zombie,DPSavvy] particle mobSpell ~ ~1 ~ 3.45 6.31 1.60 0.1 0
	@e[$SelectArena$,type=Zombie,DPSavvy] particle mobSpell ~ ~1 ~ 3.45 6.31 1.60 0.1 0
END
//>

//<Mob Testfor Clock: Chicken
FUNC Func_MobTFClock_Chicken
	testfor @e[type=Chicken,DPChicken]
	SetblockTestfor
	
	@e[$SA$,type=AreaEffectCloud,DPti>=100<=105] entitydata @e[type=Chicken,DPChicken] {Silent:1}
	@e[$SelectFloor$,type=Chicken,DPChicken] DPmo = 1 {OnGround:1b}
	$s$kill @e[$SelectFloor$,type=Chicken,DPChicken,DPmo=1]
END
//>

//<Mob Testfor Clock: Fire
FUNC Func_MobTFClock_Fire
	testfor @e[DPFire]
	SetblockTestfor
	
	@e[$SelectArena$,type=Zombie,DPFire] particle flame ~ ~1 ~ 0.1 0.7 0.1 0.05 1 force @a
	@e[$SelectArena$,type=Blaze,DPFire] particle flame ~ ~1 ~ 0.1 0.5 0.1 0.03 1 force @a
	@e[$SelectArena$,type=Creeper,DPFire] particle flame ~ ~1 ~ 0.1 0.7 0.1 0.05 1 force @a
	@e[$SelectArena$,type=Spider,DPFire] particle flame ~ ~ ~ 0.1 0 0.1 0.05 1 force @a
END
//>

//<Mob Testfor Clock: Blaze Turret
FUNC Func_MobTFClock_BlazeTurret
	testfor @e[type=Blaze,DPFire]
	SetblockTestfor
	
	@e[$SelectArena$,type=Blaze,DPFire] DPmo + 0 {OnGround:1b}
	@e[$SelectArena$,type=Blaze,DPFire] DPmo2 + 0
	@e[$SelectArena$,type=Blaze,DPFire,DPmo>=0<=4] DPmo + 1
	
	//(Reduce health to 175 or make invulnerable until all are killed)
	
	@e[$SelectArena$,type=Blaze,DPFire,DPmo=4] summon SmallFireball ~ ~ ~1 {direction:[0d,0d,0.15d],power:[0d,0d,0.1d],Tags:["DPMob","DPBlazeTurretFireball"]}
	@e[$SelectArena$,type=Blaze,DPFire,DPmo=4] summon SmallFireball ~ ~ ~-1 {direction:[0d,0d,-0.15d],power:[0d,0d,-0.1d],Tags:["DPMob","DPBlazeTurretFireball"]}
	
	@e[$SelectArena$,type=Blaze,DPFire,DPmo=5] summon SmallFireball ~ ~1 ~1 {direction:[0d,0d,0.15d],power:[0d,0d,0.1d],Tags:["DPMob","DPBlazeTurretFireball"]}
	@e[$SelectArena$,type=Blaze,DPFire,DPmo=5] summon SmallFireball ~ ~1 ~-1 {direction:[0d,0d,-0.15d],power:[0d,0d,-0.1d],Tags:["DPMob","DPBlazeTurretFireball"]}
	
	@e[$SelectArena$,type=Blaze,DPFire] ~ ~ ~ effect @a[DPpl>=1,dy=1] 7 1 0 true
	@e[$SelectArena$,type=Blaze,DPFire,DPmo=5] DPmo = 0
	@e[$SelectArena$,type=Blaze,DPFire,DPmo>=1] fill $FireZ$ fire 0 replace air 0
	
	@e[$SelectArena$,type=Blaze,DPFire,DPmo2>=0<=71] DPmo2 + 1
	@e[$SelectArena$,type=Blaze,DPFire,DPmo2>=72] DPmo2 = 1
	
	LOOP 1 36 + 1 () 5 360 + 10.0 () 185 540 + 10.0
		///	say Tick |0| Angle |2| OppAngle |4| YAngle |6|
		@e[$SelectArena$,type=Blaze,DPFire,DPmo2=|0|] 'particle flame ~ ~0.1 ~ 'MUL(SIN(|2|);$mod$)' 0 'MUL(COS(|2|);$mod$)' 0.1 0 force'
		@e[$SelectArena$,type=Blaze,DPFire,DPmo2=|0|] 'particle flame ~ ~0.1 ~ 'MUL(SIN(|4|);$mod$)' 0 'MUL(COS(|4|);$mod$)' 0.1 0 force'
	END
END
//>


//<Groups
Func_CMDSave()

:CBPMainStart PULSE $Style$
Func_SpawnStandStart()
Deactivate(CBPMainStart)
Func_GeneralScoreboardStart()
Activate(CBPMainClock)
Activate(CBPGeneralOptionsTFClock)
Activate(CBPSummonMobTFClock)
Activate(CBPCountdownTFClock)
Activate(CBPSpawnMobTFClock)
Activate(CBPEqupimentTFClock)
Activate(CBPMobTFClock_Resurrection)
Activate(CBPMobTFClock_Jade)
Activate(CBPMobTFClock_SmellySavvy)
Activate(CBPMobTFClock_Chicken)
Activate(CBPMobTFClock_Fire)
Activate(CBPMobTFClock_BlazeTurret)
FinalSpawnStand


:CBPOpenDoorsStart PULSE $Style$
Deactivate(CBPOpenDoorsStart)
Func_OpenDoorsStart()
Activate(CBPOpenDoorsClock)

:CBPOpenDoorsClock CLOCK $Style$
Func_OpenDoorsClock()

:CBPOpenDoorsEnd PULSE $Style$
Deactivate(CBPOpenDoorsEnd)
Func_OpenDoorsEnd()
Deactivate(CBPOpenDoorsClock)


:CBPMainClock CLOCK $Style$
Func_SpawnIDSetting()
Func_GeneralScoreboardClock()

:CBPGeneralOptionsTFClock CLOCK $Style$
Func_GeneralOptionsTFClock()

:CBPSummonMobTFClock CLOCK $Style$
Func_SummonMobTFClock()

:CBPCountdownTFClock CLOCK $Style$
Func_CountdownTFClock()

:CBPSpawnMobTFClock CLOCK $Style$
Func_SpawnMobTFClock()

:CBPEqupimentTFClock CLOCK $Style$
Func_EqupimentTFClock()

:CBPMobTFClock_Resurrection CLOCK $Style$
Func_MobTFClock_Resurrection()

:CBPMobTFClock_Jade CLOCK $Style$
Func_MobTFClock_Jade()

:CBPMobTFClock_SmellySavvy CLOCK $Style$
Func_MobTFClock_SmellySavvy()

:CBPMobTFClock_Chicken CLOCK $Style$
Func_MobTFClock_Chicken()

:CBPMobTFClock_Fire CLOCK $Style$
Func_MobTFClock_Fire()

:CBPMobTFClock_BlazeTurret CLOCK $Style$
Func_MobTFClock_BlazeTurret()


:CBPMainEnd PULSE $Style$
Func_SpawnStandEnd
Deactivate(CBPMainEnd)
Func_GeneralScoreboardEnd()
Activate(CBPOpenDoorsEnd)
Deactivate(CBPMainClock)
Deactivate(CBPGeneralOptionsTFClock)
Deactivate(CBPSummonMobTFClock)
Deactivate(CBPCountdownTFClock)
Deactivate(CBPSpawnMobTFClock)
Deactivate(CBPEqupimentTFClock)
Deactivate(CBPMobTFClock_Resurrection)
Deactivate(CBPMobTFClock_Jade)
Deactivate(CBPMobTFClock_SmellySavvy)
Deactivate(CBPMobTFClock_Chicken)
Deactivate(CBPMobTFClock_Fire)
Deactivate(CBPMobTFClock_BlazeTurret)
//>











///	give @p minecraft:potion 1 0 {CustomPotionEffects:[{Id:7,Amplifier:0,Duration:1},{Id:10,Amplifier:1,Duration:500}],display:{Name:"wat"},Unbreakable:1}





















