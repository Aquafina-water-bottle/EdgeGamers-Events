//#-p stonebrick 0 -b 232 26 -200 -print
///	C:\Users\Austin\Documents\Austin\powder game code\eGO\Events & Commands\Races\1.8R - 1.8 Race\CBP_18Race.txt
//<Defines
DEFINE $CMDName$ 18R
DEFINE $CMDSizeX$ 16
DEFINE $CMDSizeY$ 16
DEFINE $CMDSizeZ$ 16
DEFINE $Style$ 0 10 10
DEFINE $SA$ x=212,y=7,z=-241,dx=129,dy=100,dz=50
///	Name
DEFINE $Name$ 18
///	Text display name
DEFINE $TD$ 1.8R
///	Hover and scoreboard name
DEFINE $TDName$ 1.8 Race
///	Scoreboard teams color
DEFINE $Color$ gold
///	Place where air is when open
DEFINE $Dao$ 249 24 -225 255 24 -219
///	Place where redstone blocks are there when open
DEFINE $Dro$ 248 22 -221 255 22 -223
///	Spawn Coords for teleporting
DEFINE $SC$ 242 26 -222 -90 0
///	Spawn Coords ID
DEFINE $SPid$ 11
///	Where laps are completed
DEFINE $lap$ x=249,y=25,z=-229,dx=6,dy=5,dz=2
///	Area where laps are reset inside the races
DEFINE $lapAF$ x=269,y=16,z=-227,dx=8,dy=2,dz=4
///	Area where laps are disabled in spawn
DEFINE $lapSP$ x=240,y=24,z=-223,dx=31,dy=4,dz=2
///	Sand Randomizer Coords
DEFINE $SandCoordsX$ 249
DEFINE $SandCoordsY$ 24
DEFINE $SandCoordsZ$ -215
///	Load Glass Structure Block Coords
DEFINE $LoadGlass$ 257 22 -211
///	Load Glass Region to replace with barrier
DEFINE $LoadGlassRegion$ 257 27 -211 288 27 -205
///	Poison area
DEFINE $PoisonArea$ x=263,y=14,z=-221,dy=4
///	Teleport player from fence gate - Region
DEFINE $TpRg$ x=256,y=26,z=-238,dy=1
///	Teleport player to area from fence gate
DEFINE $TpTo$ 250 26 -238 0 0
///	Teleport player from underwater area
DEFINE $UnderwaterTP$ 264 16 -226 -90 0
///	Summon gold armor stands
DEFINE $UnderwaterGold1$ 260 20 -216
DEFINE $UnderwaterGold2$ 267 20 -213
DEFINE $UnderwaterGold3$ 270 20 -215
DEFINE $UnderwaterGold4$ 264 20 -218
DEFINE $UnderwaterGold5$ 274 20 -229
DEFINE $UnderwaterGold6$ 270 20 -227
DEFINE $UnderwaterGold7$ 266 20 -228
DEFINE $UnderwaterGold8$ 262 20 -228
DEFINE $UnderwaterGold9$ 261 20 -221
DEFINE $UnderwaterGold10$ 257 20 -228
///	Gold armor stand
DEFINE $ArmorStandGold$ {Tags:["18Race","18RaceGold"],DisabledSlots:2096896,Invulnerable:1,PersistenceRequired:1,Invisible:1,NoGravity:1,ArmorItems:[{},{},{},{id:gold_block,Count:1b}]}
///	Sand
DEFINE $RefreshSand$ 239 22 -211 239 6 -211
/// Races
DEFINE $s$ minecraft:
DEFINE $ObjA$ scoreboard objectives add
DEFINE $ObjR$ scoreboard objectives remove
DEFINE $TeamA$ scoreboard teams add
DEFINE $TeamO$ scoreboard teams option
DEFINE $TeamR$ scoreboard teams remove
DEFINE $TextStart$ {"text":"","extra":[{"text":"[","color":"gray"},{"text":"PARSE($TD$)","color":"PARSE($Color$)","bold":"true","hoverEvent":{"action":"show_text","value":{"text":"PARSE($TDName$)","color":"PARSE($Color$)"}}},{"text":"]","color":"gray"},{"text":": "},
DEFINE $SPpvp$ 1
DEFINE $SPsat$ 1
DEFINE Deactivate fill |0| stonebrick 0
DEFINE Activate fill |0| redstone_block 0
DEFINE ColorFillWool fill |0| wool |1|
DEFINE ColorFillClay fill |0| stained_hardened_clay |1|
DEFINE ColorFillGlass fill |0| stained_glass |1|
DEFINE ColorFillWood fill |0| planks |1|
DEFINE ScTG scoreboard players tag
DEFINE ScOP scoreboard players operation
DEFINE ScTE scoreboard players test
DEFINE ScRE scoreboard players reset
DEFINE ScTJ scoreboard teams join
DEFINE ScTL scoreboard teams leave
DEFINE RevST scoreboard players tag @a[|0|] remove |0|
DEFINE SetblockTestfor setblock ~ ~ ~ repeating_command_block 11 replace {TrackOutput:0b,auto:1b}
DEFINE ExecuteSpawnStand @e[type=ArmorStand,SpawnStand,SPgam=0]
DEFINE FinalSpawnStand @e[type=ArmorStand,SpawnStand,SPgam=0] SPgam = PARSE($SPid$)
DEFINE NumberAdd |0| Number = |0|
DEFINE NumberObjAdd Number dummy
FUNC Func_CMDSave
fill 0x -1y 0z 3x -3y 3z air 0
setblock 1x -1y 1z structure_block 0 replace {name:"CMDSave_$CMDName$",mode:"SAVE",posX:0,posY:2,posZ:0,sizeX:$CMDSizeX$,sizeY:$CMDSizeY$,sizeZ:$CMDSizeZ$}
setblock 1x -3y 1z structure_block 0 replace {name:"CMDSave_$CMDName$",mode:"LOAD",posX:0,posY:4,posZ:0}
setblock 1x -1y 0z wooden_button 4
setblock 1x -3y 0z wooden_button 4
setblock 2x -1y 1z redstone_lamp 0
setblock 2x -3y 1z redstone_lamp 0
setblock 1x -1y 2z minecraft:command_block 3 replace {TrackOutput:0b,Command:"setblock ~1 ~ ~ redstone_block 0"}
END
FUNC Func_SpawnIDSetting
@a[$SA$] SPid + 0
@a[$SA$,SPid<-$SPid$] SPid = $SPid$
@a[$SA$,SPid>-$SPid$] SPid = $SPid$
END
FUNC Func_SpawnStandStart
@e[type=ArmorStand,SpawnStand] SPtp = $SPid$
@e[type=ArmorStand,SpawnStand] SPpvp = $SPpvp$
@e[type=ArmorStand,SpawnStand] SPsat = $SPsat$
@e[type=ArmorStand,SpawnStand] SPgmd = 1
@e[type=ArmorStand,SpawnStand,SPgam>=1] SPgac = 1
END
FUNC Func_SpawnStandEnd
@e[type=ArmorStand,SpawnStand] SPtp = 0
@e[type=ArmorStand,SpawnStand] SPpvp = 0
@e[type=ArmorStand,SpawnStand] SPsat = 1
@e[type=ArmorStand,SpawnStand] SPgmd = 1
@e[type=ArmorStand,SpawnStand,SPgam=$SPid$] SPgam = 0
@e[type=ArmorStand,SpawnStand] SPwea = 0
END
FUNC Func_SpawnStandEdit
@e[type=ArmorStand,SpawnStand] SPtp = |0|
@e[type=ArmorStand,SpawnStand] SPpvp = |1|
@e[type=ArmorStand,SpawnStand] SPsat = |2|
@e[type=ArmorStand,SpawnStand] SPgmd = |3|
@e[type=ArmorStand,SpawnStand,SPgam>=1] SPgac = 1
@e[type=ArmorStand,SpawnStand] SPwea = |4|
END
//>
//<Comments
FUNC Func_Comments
1.8r
Clocks:
-1 teleport
-1 that retains invisibility
-1 that kills you on the sand
-1 that kills you when you are 2 blocks above any hay
-1 that loops around the 5 sand layouts - every 10s (200mt)
-1 that will switch around 5 different barrier layouts, connected to the one for the sand
-1 that gives poison to the player every 1/5 time for 20 seconds
-1 that changes the layout of the underwater area every minute - 1200mt
-Has countdown with an invisible armor stand from 5-1
-Turns on redstone lamp when it is at 5
-Playsounds an explosion on where the original closed, and another softer sound to where it opened
260 20 -216
267 20 -213
270 20 -215
264 20 -218
274 20 -229
270 20 -227
266 20 -228
262 20 -228
261 20 -221
257 20 -228
END
//>
//<To Do
FUNC Func_ToDoThings
TDWorks TDTest TDFix
Mandatory Unimplemented Changes:
-
General Unimplemented Changes:
-
Bugs:
-
Implemented Changes:
-
Bug fixes:
-
END
//>
//<General Scoreboard Start
FUNC Func_EditGeneralScoreboardStart
USE $ObjA$
18RaceTimer1 dummy 1.8 Race Timer - 40
18RaceTimer2 dummy 1.8 Race Timer - 200
18RaceTimer3 dummy 1.8 Race Timer - 80
18RaceRand dummy 1.8 Race Randomizer
18RaceTP dummy 1.8 Race Teleport
UNUSE
///	Other
gamerule naturalRegeneration false
///	Timer Armor Stand
ExecuteSpawnStand summon ArmorStand ~ ~ ~ {Tags:["18Race","18RaceTimer"],Invulnerable:1,PersistenceRequired:1,Invisible:1,Marker:1,NoGravity:1}
@e[type=ArmorStand,18RaceTimer] 18RaceTimer1 + 0
@e[type=ArmorStand,18RaceTimer] 18RaceTimer2 + 0
///	Sand Randomizer
LOOP 1 5 + 1 () 0 4 + 1
ExecuteSpawnStand summon AreaEffectCloud $SandCoordsX$ SUB($SandCoordsY$;|2|) $SandCoordsZ$ {Tags:["18Race","18RaceRand","18RaceSand","18RaceSand|0|"],Duration:1000000}
END
///	Golden Block Randomizer
LOOP 1 10 + 1
ExecuteSpawnStand summon AreaEffectCloud $UnderwaterGold|0|$ {Tags:["18Race","18RaceRand","18RaceWater","18RaceWater|0|"],Duration:1000000}
END
@e[type=AreaEffectCloud,18RaceRand] 18RaceRand + 0
@r[type=AreaEffectCloud,18RaceWater] 18RaceRand = 1
///	Refresh sand
fill $RefreshSand$ redstone_block 0 replace stonebrick 0
fill $RefreshSand$ stonebrick 0 replace redstone_block 0
END
//>
//<Main Clock
FUNC Func_EditGeneralScoreboardClock
///	Teleport
@a[$TpRg$,m=2,team=$Name$R] 18RaceTP = 2
$s$tp @a[$SA$,m=2,team=$Name$R,18RaceTP=2] $TpTo$
@a[$SA$,m=2,team=$Name$R] sand -1 ~ ~-0.2 ~ $s$kill @a[c=1]
///	Poison
@e[type=ArmorStand,18RaceTimer,18RaceTimer1>=0<=39] 18RaceTimer1 + 1
@e[type=ArmorStand,18RaceTimer,18RaceTimer1=40] effect @a[$PoisonArea$,m=2,team=$Name$R] 19 20 3 true
@e[type=ArmorStand,18RaceTimer,18RaceTimer1=40] 18RaceTimer1 = 0
///	Change sand and barrier block formation
@e[type=ArmorStand,18RaceTimer,18RaceTimer2>=0<=199] 18RaceTimer2 + 1
@e[type=ArmorStand,18RaceTimer,18RaceTimer2=186] fill $LoadGlassRegion$ air 0
@e[type=ArmorStand,18RaceTimer,18RaceTimer2=187] setblock $LoadGlass$ redstone_block 0
@e[type=ArmorStand,18RaceTimer,18RaceTimer2=187] setblock $LoadGlass$ stonebrick 0
@e[type=ArmorStand,18RaceTimer,18RaceTimer2=192] fill $LoadGlassRegion$ barrier 0 replace glass 0
@e[type=ArmorStand,18RaceTimer,18RaceTimer2=196] fill $LoadGlassRegion$ glass 0 replace barrier 0
@e[type=ArmorStand,18RaceTimer,18RaceTimer2=200] fill $LoadGlassRegion$ barrier 0 replace glass 0
@e[type=ArmorStand,18RaceTimer,18RaceTimer2=200] @r[type=AreaEffectCloud,18RaceSand,18RaceRand=0] 18RaceRand = 1
@e[type=ArmorStand,18RaceTimer,18RaceTimer2=200] @e[type=AreaEffectCloud,18RaceSand,18RaceRand=1] setblock ~ ~ ~ redstone_block 0
@e[type=ArmorStand,18RaceTimer,18RaceTimer2=200] @e[type=AreaEffectCloud,18RaceSand,18RaceRand=1] setblock ~ ~ ~ stonebrick 0
@e[type=ArmorStand,18RaceTimer,18RaceTimer2=200] @e[type=AreaEffectCloud,18RaceSand,18RaceRand=2] 18RaceRand = 0
@e[type=ArmorStand,18RaceTimer,18RaceTimer2=200] @e[type=AreaEffectCloud,18RaceSand,18RaceRand=1] 18RaceRand = 2
@e[type=ArmorStand,18RaceTimer,18RaceTimer2=200] 18RaceTimer2 = 0
///	Golden block Randomizer
$s$tp @e[type=ArmorStand,18RaceGold] ~ ~ ~ ~2 ~
@a[$SA$,m=2,team=$Name$R] @e[r=1,c=1,type=AreaEffectCloud,18RaceWater,18RaceRand=2] 18RaceRand = 3
@e[type=AreaEffectCloud,18RaceWater,18RaceRand=3] @a[r=1,c=1,m=2,team=$Name$R] 18RaceTP = 1
$s$tp @a[$SA$,m=2,team=$Name$R,18RaceTP=1] $UnderwaterTP$
@e[type=AreaEffectCloud,18RaceWater,18RaceRand=3] ~ ~-1 ~ $s$kill @e[r=1,c=1,type=ArmorStand,18RaceGold] $UnderwaterTP$
@e[type=AreaEffectCloud,18RaceWater,18RaceRand=3] @r[type=AreaEffectCloud,18RaceWater,18RaceRand=0] 18RaceRand = 1
@e[type=AreaEffectCloud,18RaceWater,18RaceRand=3] 18RaceRand = 0
@e[type=AreaEffectCloud,18RaceWater,18RaceRand=1] summon ArmorStand ~ ~-1 ~ $ArmorStandGold$
@e[type=AreaEffectCloud,18RaceWater,18RaceRand=1] playsound minecraft:entity.experience_orb.touch voice @a[$SA$]
@e[type=AreaEffectCloud,18RaceWater,18RaceRand=1] 18RaceRand = 2
///	Regeneration
effect @a[$SA$,m=2,team=$Name$R] 10 0 0 true
@a[$SA$,m=2,team=$Name$R] 18RaceTimer3 + 1
effect @a[$SA$,m=2,team=$Name$R,18RaceTimer3=80] 10 1 10 true
@a[$SA$,m=2,team=$Name$R,18RaceTimer3=80] 18RaceTimer3 = 0
///	TP End
@a[$SA$,m=2,team=$Name$R,18RaceTP=1] playsound minecraft:entity.player.levelup voice @a[c=1]
@a[$SA$,m=2,team=$Name$R,18RaceTP=2] playsound minecraft:entity.endermen.teleport voice @a[c=1]
@a[$SA$,m=2,team=$Name$R,18RaceTP>=1] particle happyVillager ~0.1 ~1 ~0.1 0.3 0.5 0.3 1 20 force
@a[$SA$,m=2,team=$Name$R,18RaceTP>=1] 18RaceTP = 0
END
//>
//<Countdown
FUNC Func_EditCountdownClock
@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=120] fill $Dao$ stonebrick 0 replace redstone_block 0
@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=120] fill $Dro$ redstone_block 0 replace stonebrick 0
END
//>
//<Main End
FUNC Func_EditGeneralScoreboardEnd
///	Scoreboard
USE $ObjR$
18RaceTimer1
18RaceTimer2
18RaceTimer3
18RaceRand
18RaceTP
UNUSE
///	Custom
$s$kill @e[type=ArmorStand,18Race]
$s$kill @e[type=AreaEffectCloud,18Race]
///	General reset
gamerule naturalRegeneration true
fill $Dao$ redstone_block 0 replace stonebrick 0
fill $Dro$ stonebrick 0 replace redstone_block 0
END
//<Main Start
FUNC Func_GeneralScoreboardStart
///	Scoreboards
USE $ObjA$
$Name$Race dummy $TDName$
$Name$RaceCD dummy $TDName$ Countdown
$Name$RaceCL dummy $TDName$ Clear
$Name$RaceDE deathCount $TDName$ Deaths
UNUSE
ExecuteSpawnStand scoreboard objectives setdisplay sidebar $Name$Race
scoreboard teams add $Name$R
scoreboard teams option $Name$R color $Color$
scoreboard teams option $Name$R seeFriendlyInvisibles true
scoreboard teams option $Name$R collisionRule never
scoreboard teams option $Name$R friendlyfire false
///	Countdown start
ExecuteSpawnStand summon AreaEffectCloud ~ ~ ~ {Duration:130,Tags:["$Name$RaceCD"]}
///	Other
\/spawn $Name$Race = 99
ScTJ $Name$R \/spawn
END
//>
//<Main Clock
FUNC Func_GeneralScoreboardClock
Func_SpawnIDSetting
$s$kill @e[$SA$,type=Item]
@a[$SA$,m=2] $Name$RaceCL + 0
@a[$SA$,m=2,$Name$RaceCL=0] $Name$RaceDE = 1
@a[$SA$,m=2,$Name$RaceCL=0] $Name$RaceCL = 1
effect @a[$SA$,m=2,$Name$RaceDE>=1] clear
$s$clear @a[$SA$,m=2,$Name$RaceDE>=1]
ScTJ $Name$R @a[$SA$,team=!$Name$R]
effect @a[$SA$,m=2,team=$Name$R] 14 3 0 true
@a[$SA$,m=2,team=$Name$R] $Name$Race + 0
ScTG @a[$lapSP$,m=2,team=$Name$R,$Name$Race>=0,!$Name$Lap] add $Name$Lap
@a[$lap$,m=2,team=$Name$R,$Name$Race>=0,!$Name$Lap] $Name$Race + 1
@a[$lap$,m=2,team=$Name$R,$Name$Race>=1,!$Name$Lap] tellraw @a[$SA$] $TextStart${"selector":"@a[c=1,r=0,m=2,team=$Name$R,$Name$Race>=1,!$Name$Lap]"},{"text":" has finished a lap!","color":"yellow"}]}
@a[$lap$,m=2,team=$Name$R,$Name$Race>=1,!$Name$Lap] playsound minecraft:entity.experience_orb.touch voice @a
ScTG @a[$lap$,m=2,team=$Name$R,$Name$Race>=0,!$Name$Lap] add $Name$Lap
ScTG @a[$lapAF$,m=2,team=$Name$R,$Name$Race>=0,$Name$Lap] remove $Name$Lap
Func_EditGeneralScoreboardClock
@a[$SA$,m=2,$Name$RaceDE>=1] $Name$RaceDE = 0
END
//>
//<Countdown
FUNC Func_CountdownClock
@e[type=AreaEffectCloud,$Name$RaceCD] $Name$RaceCD + 1
@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=20] tellraw @a[$SA$] $TextStart${"text":"5","color":"yellow","bold":"true"}]}
@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=20] @a[$SA$] playsound block.note.pling voice @a[c=1] ~ ~ ~ 0.5
@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=40] tellraw @a[$SA$] $TextStart${"text":"4","color":"yellow","bold":"true"}]}
@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=40] @a[$SA$] playsound block.note.pling voice @a[c=1] ~ ~ ~ 1
@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=60] tellraw @a[$SA$] $TextStart${"text":"3","color":"yellow","bold":"true"}]}
@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=60] @a[$SA$] playsound block.note.pling voice @a[c=1] ~ ~ ~ 1.5
@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=80] tellraw @a[$SA$] $TextStart${"text":"2","color":"yellow","bold":"true"}]}
@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=80] @a[$SA$] playsound block.note.pling voice @a[c=1] ~ ~ ~ 2
@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=100] tellraw @a[$SA$] $TextStart${"text":"1","color":"yellow","bold":"true"}]}
@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=100] @a[$SA$] playsound block.note.pling voice @a[c=1] ~ ~ ~ 2
@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=120] @a[$SA$] playsound minecraft:entity.wither.death voice @a[c=1]
@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=120] tellraw @a[$SA$] $TextStart${"text":"GO!","color":"green","bold":"true"}]}
@e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=120] title @a[$SA$] title {"text":"GO!","color":"green"}
testfor @e[type=AreaEffectCloud,$Name$RaceCD,$Name$RaceCD=120]
CONDITION
Deactivate(CBPCountdown)
END
//>
//<Main End
FUNC Func_GeneralScoreboardEnd
///	Scoreboard
USE $ObjR$
$Name$Race
$Name$RaceCD
$Name$RaceCL
$Name$RaceDE
UNUSE
scoreboard teams remove $Name$R
ScTG * remove $Name$Lap
///	Countdown reset
$s$kill @e[type=AreaEffectCloud,$Name$RaceCD]
///	General reset
effect @a[$SA$,m=2] clear
$s$clear @a[$SA$,m=2]
effect @a[$SA$,m=2] 6 20 100 true
@a[$SA$,m=2] SPtp = $SPid$
END
//>
//<Groups
Func_CMDSave
:CBPMainStart PULSE $Style$
Func_SpawnStandStart
Deactivate(CBPMainStart)
Func_GeneralScoreboardStart
Func_EditGeneralScoreboardStart
Activate(CBPMainClock)
Activate(CBPCountdown)
FinalSpawnStand
:CBPMainClock CLOCK $Style$
Func_GeneralScoreboardClock
:CBPCountdown CLOCK $Style$
Func_CountdownClock
Func_EditCountdownClock
:CBPMainEnd PULSE $Style$
Func_SpawnStandEnd
Deactivate(CBPMainEnd)
Func_GeneralScoreboardEnd
Func_EditGeneralScoreboardEnd
Deactivate(CBPMainClock)
Deactivate(CBPCountdown)
//>
//>
