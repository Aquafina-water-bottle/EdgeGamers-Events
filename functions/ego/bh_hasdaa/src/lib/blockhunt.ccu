# Just a slightly modified virus.ccu
$py(r"""
Copy/pasta the following:

$py(
from lib.virus import *
from lib.floo_network import VIRUS_NAME
from lib.coords import Coords

# all virus maps must have a virus object
virus = Virus(VIRUS_NAME,
    select_spawn=Coords(""),
    wait_coords=Coords(""),
    select_virus=Coords(""))

# virus_door = Coords("")
# mid_door = Coords("")

)

# additional commands for the main start
$macro(edit_round_start)
#    fill $(str(mid_door)) stonebrick 0 replace redstone_block 0
$endmacro

# additional commands for the main clock
$macro(edit_countdown_end)
#    fill $(str(virus_door)) stonebrick 0 replace redstone_block 0
#    fill $(str(mid_door)) redstone_block 0 replace stonebrick 0
$endmacro

$macro(edit_round_end)
#    fill $(str(virus_door)) redstone_block 0 replace stonebrick 0
#    fill $(str(mid_door)) redstone_block 0 replace stonebrick 0
$endmacro

# additional commands for the countdown clock
$macro(edit_round_reset)
#    fill $(str(virus_door)) redstone_block 0 replace stonebrick 0
#    fill $(str(mid_door)) redstone_block 0 replace stonebrick 0
$endmacro

# additional commands for the main end
$macro(edit_main_end)
#    fill $(str(virus_door)) redstone_block 0 replace stonebrick 0
#    fill $(str(mid_door)) redstone_block 0 replace stonebrick 0
$endmacro

$include("lib/blockhunt.ccu")
""")

$py(
from lib.consts import Effects, Sounds, Enchants
from lib.scoreboard import OBJECTIVES, TEAMS
from lib.const_ints import CONST_INTS
from lib.prng import PRNG

# general constants
select_all = virus.event.select_all

# the initials are the same as the display objective name
initials = obj_name = virus.initials

# shorthand team names
virus_team = virus.initials + "v"
hiders_team = virus.initials + "h"

prng = PRNG(virus.event, initials + "prng", 0, 10)

OBJECTIVES.new_str("""
    # The main display objective
    . _ .

    # Holds all of the players to initialize
    # 0 = being outside the map
    # 1 = part of the virus map, meaning the players are properly initialized
    pl _ Player List

    # Sets the score depending on whether they are with the map or not
    # 0 = not in the map
    # 1 = outside the map
    sa _ Select All

    # Used on the armor stand to count down the time on both the
    # countdown and the game time
    ti _ Timer

    # Used on the armor stand to count how many hiders and virus
    # players exist
    chi _ Count Hiders
    cvr _ Count Virus

    # Used on the armor stand to show whether the players glow or not
    # 0 = None
    # 1 = Virus
    # 2 = Hiders
    # 3 = Both
    gl _ Glowing Players

    # Used on the armor stand for misc. calculations
    calc _ Calculations

    # used on the Virus Stand to store the state of the round
    # 0 = waiting for the round to start
    # 1 = round start
    # 2 = during countdown
    # 3 = after countdown, main game
    # 4 = round end
    # 5 = during round end
    # 6 = reset round --> waiting
    st _ State
    # xp _ XP Calc

""", initials, virus.obj_disp)

OBJECTIVES[initials].setdisplay("sidebar")

CONST_INTS.add_consts(20, 60, 1200)

TEAMS.new_str("""
    h Hiders
        friendlyfire false
        collisionRule never
        deathMessageVisibility never
        color green
        nametagVisibility hideForOtherTeams
    
    v Virus
        friendlyfire false
        collisionRule never
        deathMessageVisibility never
        color yellow
    
    d_y Display Yellow
        color yellow
    
    d_g Display Green
        color green
""", initials, virus.obj_disp)

)

$include("lib/prng.ccu")
$create_prng_init(virus.event, initials + "prng")

!mfunc init
    $(prng.cmd_init())
    $(virus.cmd_init())
    $(OBJECTIVES.cmd_init())
    $(TEAMS.cmd_init())

    join $(initials)d_y Countdown Minutes Seconds Seekers
    join $(initials)d_g Hiders

    $(initials)Countdown $(initials)calc = $(20 * 60)
    $(initials)Glowing $(initials)calc = $(20 * 300)
    $(initials)GameTime $(initials)calc = $(20 * 600)

    summon armor_stand ~ ~ ~ {Tags:["$(initials)Stand","$(initials)Entity"],Invulnerable:1,PersistenceRequired:1,Invisible:1,Marker:1,NoGravity:1}
    @e[type=armor_stand,$(initials)Stand] function reset_round
    $(virus.cmd_post_init())

!mfunc main
    $(virus.cmd_main())

    # kills all items
    kill @e[$(select_all),tag=!$(initials)Entity,type=item]

    # resets the player if they die
    @a[m=2,gDE=1,$(initials)pl=1] $(initials)pl = 0

    # also resets the player if they just rejoined the game
    @a[m=2,gLG=1,$(initials)pl=1] function assign_teams

    # @e[type=armor_stand,$(initials)Stand,$(initials)st=0] function wait_for_start
    @e[type=armor_stand,$(initials)Stand,$(initials)st=1] function start_round
    @e[type=armor_stand,$(initials)Stand,$(initials)st=2] function countdown
    @e[type=armor_stand,$(initials)Stand,$(initials)st=3] function during_round
    @e[type=armor_stand,$(initials)Stand,$(initials)st=4] function stop_round
    @e[type=armor_stand,$(initials)Stand,$(initials)st=5] function wait_for_reset
    @e[type=armor_stand,$(initials)Stand,$(initials)st=6] function reset_round

    # sets the glowing effect
    @e[type=armor_stand,$(initials)Stand] function set_glowing_effects
    
    # shows the player numbers at all times
    @e[type=armor_stand,$(initials)Stand] function calc_player_numbers
    
    # initializes the player
    @a $(initials)pl + 0
    @a[gSA=1,m=2,$(initials)pl=0] function assign_teams

    # teleports any players to the given location as necessary
    @a[m=2,$(initials)pl=1] function teleport_players

!mfunc term
    $(virus.cmd_term())

    @a[$(initials)pl=1,m=2] function full_reset_player

    kill @e[type=armor_stand,$(initials)Entity]

    $(OBJECTIVES.cmd_term())
    $(TEAMS.cmd_term())


# !mfunc wait_for_start


# used on the Virus Stand to start the game
!mfunc start_round
    # opens the doors
    # title @a actionbar {"text":"The doors are now open","color":"green"}
    tp @a[$(virus.select_spawn),team=$(initials)h] str($(virus.arena_coords))
    @a playsound $(Sounds.level) voice @s
    $edit_round_start()

    # sets the round time
    ScOP @s $(initials)ti = $(initials)Countdown $(initials)calc

    # sets the state
    @s $(initials)st = 2


!mfunc countdown
    # runs the timer
    function timer
    
    # sets the player effects
    @a[$(initials)pl=1,m=2] function player_effects

    @s[$(initials)ti=..0] function end_countdown


!mfunc end_countdown
    Countdown reset $(obj_name)
    title @a actionbar {"text":"The seeker has been released!","color":"yellow"}
    tp @a[$(virus.select_virus),team=$(initials)v] str($(virus.arena_coords))
    @a playsound $(Sounds.wither) voice @s
    $edit_countdown_end()
    
    ScOP @s $(initials)ti = $(initials)GameTime $(virus.initials)calc
    @s $(initials)st = 3


!mfunc during_round
    # runs the timer
    function timer

    # sets all hiders to virus if they stay in spawn
    @a[$(virus.select_spawn),m=2,team=$(hiders_team)] function assign_teams

    # sets the player effects
    @a[gSA=1,m=2,$(initials)pl=1] function player_effects
    function calc_glowing

    # detects whether the virus won
    @e[type=armor_stand,$(initials)Stand,$(initials)chi=0] $(initials)st = 4


# used on players to set their effects
!mfunc player_effects
    effect @s[team=$(virus_team)] $(Effects.strength) 3 90 true
    effect @s $(Effects.resist) 3 3 true
    effect @s $(Effects.hp) 100 100 true

!mfunc invuln_effect
    effect @s $(Effects.resist) 3 10 true
    effect @s $(Effects.hp) 100 100 true

# used on the Virus Stand to set the glowing effect
!mfunc set_glowing_effects
    @s[$(initials)gl=2..] effect @a[gSA=1,m=2,team=$(hiders_team)] $(Effects.glow) 3 0 true
    @s[$(initials)gl=2..] $(initials)gl - 2
    @s[$(initials)gl=1..] effect @a[gSA=1,m=2,team=$(virus_team)] $(Effects.glow) 3 0 true
    @s[$(initials)gl=2..] $(initials)gl - 1


# used on the Virus Stand when there are no more hiders or when the timer runs out
!mfunc stop_round
    # all players will glow
    @s $(initials)gl = 3

    # if the virus won, the hider number is 0
    @s[$(initials)chi=0] title @a title {"text":"The virus won!","color":"yellow"}
    @s[$(initials)chi=0] tellraw @a $(virus.event.begin(r'{"text":"The seekers won!","color":"yellow"}'))

    # if the hiders won, then is more than one hider
    @s[$(initials)chi=1..] title @a title {"text":"Time!","color":"green"}
    @s[$(initials)chi=1..] title @a subtitle {"text":"The hiders won!","color":"green"}
    @s[$(initials)chi=1..] tellraw @a $(virus.event.begin(r'{"text":"The time is up! Hiders win!","color":"yellow"}'))
    @s[$(initials)chi=1..] Seconds reset $(obj_name)
    @s[$(initials)chi=1..] Minutes reset $(obj_name)
    $edit_round_end()

    @s $(initials)st = 5


# used on the Virus Stand when waiting for the round reset
!mfunc wait_for_reset
    @a[gSA=1,m=2,$(initials)pl=1] function invuln_effect


# used on the Virus Stand when the round actually resets
!mfunc reset_round
    @a[gSA=1,m=2,$(initials)pl=1] function full_reset_player
    @s $(initials)gl = 1
    $edit_round_reset()
    
    Countdown reset $(obj_name)
    Minutes reset $(obj_name)
    Seconds reset $(obj_name)
    @s $(initials)st = 0


# used on a given player to initialize and clear their inventory
!mfunc reset_player
    clear @s
    effect @s clear
    $(virus.cmd_book("@s[EC=0]"))

    # sets their player state to 1 to indicate they are initialized
    @s $(initials)pl = 1

# used on a given player to have a during a round, which covers
# default teleporting and setting of teams
!mfunc assign_teams
    function reset_player
    function to_hider if @e[type=armor_stand,$(initials)Stand,$(initials)st=0..2]
    function to_virus if @e[type=armor_stand,$(initials)Stand,$(initials)st=3]
    function to_hider if @e[type=armor_stand,$(initials)Stand,$(initials)st=4..6]

# used on a given player to reset them completely,
# meant to be used at the end of a round
!mfunc full_reset_player
    function reset_player
    function tp_to_spawn
    function to_hider


# used on players to add them to the hider team
!mfunc to_hider
    $(prng.cmd_assign())
    clear @s
    effect @s clear
    $(virus.cmd_book("@s[EC=0]"))
    
    join $(hiders_team) @s

    # teleports back to the spawn if the game hasn't started yet
    function tp_to_spawn if @e[type=armor_stand,$(initials)Stand,$(initials)st=0..2]


# used on players to add them to the virus team
!mfunc to_virus
    clear @s
    effect @s clear
    $(virus.cmd_book("@s[EC=0]"))

    # adds a golden helmet lol
    replaceitem entity @s slot.armor.head golden_helmet 1 0 {
        Unbreakable:1,ench:[{id:$(Enchants.BIND),lvl:1},{id:$(Enchants.VANISH),lvl:1}]}

    title @s title {"text":"You are now","color":"yellow"}
    title @s subtitle {"text":"the seeker!","color":"yellow"}
    join $(virus_team) @s

    # teleports to the waiting room during countdown
    function tp_to_wait if @e[type=armor_stand,$(initials)Stand,$(initials)st=0..2]

    # teleports to the arena during game time
    function tp_to_arena if @e[type=armor_stand,$(initials)Stand,$(initials)st=3..6]


# used on players to make sure that players that are in spawn will be properly teleported
!mfunc teleport_players
    # teleports to the waiting room during countdown
    @s[$(virus.select_spawn),team=$(virus_team)] function tp_to_wait if @e[type=armor_stand,$(initials)Stand,$(initials)st=0..2]

    # teleports to the arena during game time
    @s[$(virus.select_spawn),team=$(virus_team)] function tp_to_arena if @e[type=armor_stand,$(initials)Stand,$(initials)st=3..6]

!mfunc tp_to_wait
    tp @s $(virus.wait_coords)

!mfunc tp_to_arena
    tp @s $(virus.arena_coords)

!mfunc tp_to_spawn
    $(virus.cmd_spawn())


# used on the Virus Stand to count time
!mfunc timer
    @s[$(initials)ti=1..] $(initials)ti - 1
    function disp_time_text
    function disp_time_score

# used on the Virus Stand to display the time in tellraw messages
!mfunc disp_time_text
    @s[$(initials)ti=$(5*60*20)] tellraw @a $(virus.event.begin(r'{"text":"5 minutes remain!","color":"yellow"}'))
    @s[$(initials)ti=$(4*60*20)] tellraw @a $(virus.event.begin(r'{"text":"4 minutes remain!","color":"yellow"}'))
    @s[$(initials)ti=$(3*60*20)] tellraw @a $(virus.event.begin(r'{"text":"3 minutes remain!","color":"yellow"}'))
    @s[$(initials)ti=$(2*60*20)] tellraw @a $(virus.event.begin(r'{"text":"2 minutes remain!","color":"yellow"}'))
    @s[$(initials)ti=$(1*60*20)] tellraw @a $(virus.event.begin(r'{"text":"1 minute remains!","color":"yellow"}'))
    
    @s[$(initials)ti=$(30*20)] tellraw @a $(virus.event.begin(r'{"text":"30 seconds remain!","color":"yellow"}'))
    @s[$(initials)ti=$(15*20)] tellraw @a $(virus.event.begin(r'{"text":"15 seconds remain!","color":"yellow"}'))
    @s[$(initials)ti=$(5*20)] tellraw @a $(virus.event.begin(r'{"text":"5!","color":"yellow"}'))
    @s[$(initials)ti=$(4*20)] tellraw @a $(virus.event.begin(r'{"text":"4!","color":"yellow"}'))
    @s[$(initials)ti=$(3*20)] tellraw @a $(virus.event.begin(r'{"text":"3!","color":"yellow"}'))
    @s[$(initials)ti=$(2*20)] tellraw @a $(virus.event.begin(r'{"text":"2!","color":"yellow"}'))
    @s[$(initials)ti=$(1*20)] tellraw @a $(virus.event.begin(r'{"text":"1!","color":"yellow"}'))
    @s[$(initials)ti=..0] $(initials)st = 4
    
# used on the Virus Stand to display the time on the sidebar
!mfunc disp_time_score
    ScOP Seconds $(initials)ti = @s $(initials)ti
    ScOP Seconds $(initials)ti /= 20 constants
    ScOP Seconds $(initials)ti %= 60 constants
    Seconds $(initials)ti + 1
    ScOP Seconds $(obj_name) = Seconds $(initials)ti

    ScOP Minutes $(initials)ti = @s $(initials)ti
    ScOP Minutes $(initials)ti /= 1200 constants
    ScOP Minutes $(obj_name) = Minutes $(initials)ti
    

# Used on the Virus Stand during the normal round to determine whether glowing
# can be removed or not, with the constant being (initials)Glowing
# it is determined when the calculated score goes positive,
# meaning the glowing score exceeds the timer (represented in seconds)
!mfunc calc_glowing
    ScOP @s $(initials)calc = $(initials)Glowing $(initials)calc
    ScOP @s $(initials)calc -= @s $(initials)ti

    @s[$(initials)calc=0] tellraw @a $(virus.event.begin(r'{"text":"Glowing has been removed from all seekers!","color":"yellow"}'))
    @s[$(initials)calc=0] @a playsound $(Sounds.xp) voice @a[c=1]
    @s[$(initials)calc=0] $(initials)gl = 0
    

# used on the Virus Stand to show the total number of virus and hiders
!mfunc calc_player_numbers
    @s $(initials)chi = 0
    @a[gSA=1,m=2,team=$(hiders_team)] @e[type=armor_stand,$(initials)Stand] $(initials)chi + 1
    ScOP Hiders $(obj_name) = @s $(initials)chi
    
    @s $(initials)cvr = 0
    @a[gSA=1,m=2,team=$(virus_team)] @e[type=armor_stand,$(initials)Stand] $(initials)cvr + 1
    ScOP Seekers $(obj_name) = @s $(initials)cvr
    

# used as input to manually start the round
!mfunc input_start_round
    @e[type=armor_stand,$(initials)Stand] $(initials)st = 1


# used as input to manually stop the round
!mfunc input_stop_round
    @e[type=armor_stand,$(initials)Stand] $(initials)st = 4
    

# used as input to manually reset the round
!mfunc input_reset_round
    @e[type=armor_stand,$(initials)Stand] $(initials)st = 6


# used as input to randomly select a virus
!mfunc input_select_rand_virus
    @r[gSA=1,m=2,team=$(hiders_team)] function to_virus


# used as input to randomly convert a virus to a hider
!mfunc input_remove_one_virus
    @r[gSA=1,m=2,team=$(virus_team)] function to_hider


# used as input to select a team
!mfunc input_select_team
    tellraw @a[EC=0] $(virus.event.begin())
        {"text":"Choose team: ","color":"gray"},
        {"text":"Hider","color":"green","italic":true,
            "clickEvent":{"action":"suggest_command","value":"/execute PLAYER_NAME ~ ~ ~ $(virus.cmd_func("to_hider"))"},
            "hoverEvent":{"action":"show_text","value":{"text":"Force someone to join the hiders","color":"green"}}
        },
        {"text":", ","color":"gray","italic":true},
        {"text":"Seeker","color":"yellow","italic":true,
            "clickEvent":{"action":"suggest_command","value":"/execute PLAYER_NAME ~ ~ ~ $(virus.cmd_func("to_virus"))"},
            "hoverEvent":{"action":"show_text","value":{"text":"Force someone to join the seekers","color":"yellow"}}
        }]}


!mfunc input_add_one_min
    @e[type=armor_stand,$(initials)Stand] $(initials)ti + $(20*60)

!mfunc input_add_30s
    @e[type=armor_stand,$(initials)Stand] $(initials)ti + $(20*30)

!mfunc input_remove_30s
    @e[type=armor_stand,$(initials)Stand] $(initials)ti - $(20*30)

!mfunc input_remove_one_min
    @e[type=armor_stand,$(initials)Stand] $(initials)ti - $(20*60)




# used on players to get the custom book
# requires additional backslashes because pyexpander is a potato
# and changes double backslashes --> single backslash
!mfunc book
    replaceitem entity @s slot.weapon.offhand written_book 1 0 {
        title:"$(virus.event.full_name) Book",author:"eGO",pages:["[\"\",
            {\"text\":\"$(virus.event.full_name) Settings\\\n\",\"bold\":true},
            {\"text\":\"\\\n\"},
            
            {\"text\":\"$(initials): \",\"color\":\"dark_gray\"},
            {\"text\":\"Start\",\"color\":\"dark_green\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("init"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Starts the blockhunt map so it can be ran\",\"color\":\"green\"}}
            },
            {\"text\":\" / \",\"color\":\"gray\"},
            {\"text\":\"Stop\\\n\",\"color\":\"red\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("term"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Ends the blockhunt map\",\"color\":\"red\"}}
            },
            
            {\"text\":\"Round: \",\"color\":\"dark_gray\"},
            {\"text\":\"Start\",\"color\":\"dark_green\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_start_round"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Starts an individual round\",\"color\":\"dark_green\"}}
            },
            {\"text\":\" / \",\"color\":\"gray\"},
            {\"text\":\"Stop\",\"color\":\"gold\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_stop_round"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Stops an individual round\",\"color\":\"gold\"}}
            },
            {\"text\":\" / \",\"color\":\"gray\"},
            {\"text\":\"Reset\\\n\\\n\",\"color\":\"red\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_reset_round"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Resets an individual round\",\"color\":\"red\"}}
            },
            
            
            {\"text\":\"Select Seeker: \",\"color\":\"dark_gray\"},
            {\"text\":\"[+]\",\"color\":\"dark_green\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_select_rand_virus"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Chooses a seeker\",\"color\":\"dark_green\"}}
            },
            
            {\"text\":\" \",\"color\":\"gray\"},
            {\"text\":\"[-]\\\n\",\"color\":\"red\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_remove_one_virus"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Removes a seeker\",\"color\":\"red\"}}
            },
            
            {\"text\":\"Edit Teams: \",\"color\":\"dark_gray\"},
            {\"text\":\"[+]\\\n\",\"color\":\"gold\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_select_team"))\"}
            },
            
            {\"text\":\"Time: \",\"color\":\"dark_gray\"},
            {\"text\":\"[+]\",\"color\":\"dark_green\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_add_one_min"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Adds 1 minute\",\"color\":\"dark_green\"}}
            },
            {\"text\":\" \",\"color\":\"gray\"},
            {\"text\":\"[+]\",\"color\":\"gold\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_add_30s"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Adds 30 seconds\",\"color\":\"gold\"}}
            },
            {\"text\":\" \",\"color\":\"gray\"},
            {\"text\":\"[-]\",\"color\":\"gold\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_remove_30s"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Removes 30 seconds\",\"color\":\"gold\"}}
            },
            {\"text\":\" \",\"color\":\"gray\"},
            {\"text\":\"[-]\",\"color\":\"red\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(virus.cmd_func("input_remove_one_min"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Removes 1 minute\",\"color\":\"red\"}}
            }
        ]"
    ]}

