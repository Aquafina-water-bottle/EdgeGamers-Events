$py(
from lib.floo_network import THE_PIT_3, FlooEvent
from lib.const_ints import CONST_INTS
from lib.scoreboard import OBJECTIVES, TEAMS
from lib.consts import Sounds, Effects
from lib.coords import Coords


# TODO
# degrading arena
# kill items
# announce winners

event = THE_PIT_3
floo_event = FlooEvent(THE_PIT_3, pvp="true")
initials = "TP"
select_all = event.select_all

select_spawn = Coords("-107 25 193 -98 30 202").selector()
select_arena = Coords("-107 6 146 -51 56 202").selector()

# teleport coords to get to the spectator area
# selection of the area around the above teleport coords
select_spectate = Coords("-111 16 204 -109 20 206").selector()

# region on top of the middle ball
mid_ball_upper_select = Coords("-81 23 172 -77 23 176").selector()

# select the walls
select_walls_west = Coords("-108 6 146 -108 38 202").selector()
select_walls_east = Coords("-50 6 146 -50 38 202").selector()
select_walls_north = Coords("-107 6 145 -51 38 145").selector()
select_walls_south = Coords("-107 6 203 -51 38 203").selector()


# teleport to the arena inside the middle ball
arena_tp = Coords("-79 18 174")

# teleport to the spectator area
spectate_tp = Coords("-110 17 205 -135 0")

# activates the structure for loading up the middle ball
activate_struct_mid = Coords("-80 7 174")


# fill region to clear out the middle ball
mid_ball_region = Coords("-77 17 172 -81 22 176")

# region to kill all players on the floor
floor_region = Coords("-107 6 146 -51 6 202")
floor_select = floor_region.selector()

# region to kill all players on the sides of the middle pillar
pillar_sides_select = Coords("-81 6 172 -77 9 176").selector()

# region to kill all players actually on the middle pillar
on_pillar_select = Coords("-82 10 171 -82 10 177").selector()


tp_to_west = Coords("~-1 ~ ~")
tp_to_east = Coords("~1 ~ ~")
tp_to_south = Coords("~ ~ ~1")
tp_to_north = Coords("~ ~ ~-1")


# region to select the entire platform
platform_region = Coords("-59 10 194 -99 10 154")

# ending position for the clone of the platform to the calculation area
deg_clone_calc_dest = Coords("-54 4 230")

# region to select the degrading platform calculations
deg_select_calc = Coords("-54 4 230 -14 4 270")

# region to activate the structure blocks for saving the finalized calculations
deg_activate_calc_save = Coords("-35 5 249 -33 5 251")

# region to activate the structure blocks for loading
# the calculated degrading platform
deg_activate_calc_load = Coords("-80 2 175 -78 2 173")



cs_to_spec_tags = r'{display:{Name:"Head to the spectator area",Lore:["Right click to use!"]}}'
cs_to_lobby_tags = r'{display:{Name:"Head to the lobby",Lore:["Right click to use!"]}}'
cs_toggle_tags = r'{display:{Name:"Toggle spectator status",Lore:["Right click to use!"]}}'

OBJECTIVES.new_str("""
    . _ .
    ti _ Timer
    cl _ Random Calcs
    af _ Affected Items
    we _ Weakness
    st _ State

    # Used on the TPStand to calculate the timer
    # for the degrading arena. This is set to -1
    # when it is not a degrading arena
    dg _ Degrading Arena

    # Used on the TPStand to use as input to get the arena type
    # 0 = reg_small, 1 = reg_large, 2 = deg_small, 3 = deg_large
    iat _ Arena Type Input

    # weapons
    # 0 = normal
    # 1 = knockback 5
    # 2 = porkchop
    # 3 = bow & arrow
    iwt _ Weapon Type Input

    # 0 = being initialized
    # 1 = has been initialized
    # 2 = in game
    # 3 = forever as spectator (changes back to 1 if in lobby)
    pl _ Player List
    
    # effects
    # ef dummy Effect
    # efc dummy Effect Calc

    # pu dummy Pumpkin
    # put dummy Pumpkin Timer
    # puc dummy Pumpkin Calc

    # at _ Arena Type
    # oas _ Arena Size
    # oat _ Arena Type
    # owe _ Weapon
    # oju _ Jump
    # omo _ Movement
    # ons _ NoSprint

    # tp _ Teleport
    # xp _ XP Calculations
    # TPtm _ Temp Calcs
    # TPop _ Option
    # TPti2 _ Timer Seconds
    # TPde deathCount Deaths
    # TPcs stat.useItem.minecraft.carrot_on_a_stick Carrot Stick
    # TPsa _ Select All
""", initials=initials, display="The Pit")

OBJECTIVES[initials].setdisplay("sidebar")

CONST_INTS.add_consts(20, 40, 100)
    
TEAMS.new_str("""
    . Players
        color green
        friendlyfire true

    d_y Display Yellow
        color yellow

    d_g Display Green
        color green
""", initials=initials, display="The Pit")



# region to save the original platform blocks
init_struct_save = Coords("-79 14 251 -79 11 251")

# the coordinates for the four structure blocks required to load in a platform
struct_load_plat_1 = Coords("-78 5 174")
struct_load_plat_2 = Coords("-79 5 175")
struct_load_plat_3 = Coords("-80 5 174")
struct_load_plat_4 = Coords("-79 5 173")

# region to activate the platform loading
activate_load_plat = Coords("-78 6 175 -80 6 173")

# all tags that are universal with all structure blocks for initializing the platform
init_struct_tags = r'sizeX:21,sizeY:1,sizeZ:21,showboundingbox:0b,mode:"LOAD",ignoreEntities:1b'

)

$macro(structs_platform_load_with, name)
    blockdata $(struct_load_plat_1) {name:"TP_$(name)"}
    blockdata $(struct_load_plat_2) {name:"TP_$(name)"}
    blockdata $(struct_load_plat_3) {name:"TP_$(name)"}
    blockdata $(struct_load_plat_4) {name:"TP_$(name)"}
$endmacro


!mfunc init
    $(floo_event.cmd_init())
    $(OBJECTIVES.cmd_init())
    $(TEAMS.cmd_init())
    
    join TPd_g Players
    join TPd_y Time_Elapsed Countdown
    
    # initializes the structure blocks for loading in the platform
    setblock $(struct_load_plat_1) structure_block 0 replace {rotation:"NONE",posX:-1,posY:5,posZ:0,$(init_struct_tags)}
    setblock $(struct_load_plat_2) structure_block 0 replace {rotation:"CLOCKWISE_90",posX:0,posY:5,posZ:-1,$(init_struct_tags)}
    setblock $(struct_load_plat_3) structure_block 0 replace {rotation:"CLOCKWISE_180",posX:1,posY:5,posZ:0,$(init_struct_tags)}
    setblock $(struct_load_plat_4) structure_block 0 replace {rotation:"COUNTERCLOCKWISE_90",posX:0,posY:5,posZ:1,$(init_struct_tags)}

    # saves the structures for the platform
    $(init_struct_save.fill("redstone_block"))
    $(init_struct_save.fill("stonebrick"))
    
    # initializes the armor stand with a regular large platform
    summon armor_stand ~ ~ ~ {Tags:["TPEntity","TPStand"],Invulnerable:1,NoGravity:1,Invisible:1,Marker:1b}
    @e[type=armor_stand,TPStand] TPst = 0
    @e[type=armor_stand,TPStand] TPiat = 1
    @e[type=armor_stand,TPStand] TPiwt = -1

    $(floo_event.cmd_post_init())
    

!mfunc main
    $(floo_event.cmd_main())

    # kills all arrows that are in the ground
    @e[type=arrow] TPcl = 1 {inGround:1b}
    kill @e[type=arrow,TPcl=1]
    
    # checks for input in changing the arena
    @e[type=armor_stand,TPStand,TPiat=0..] function change_arena
    @e[type=armor_stand,TPStand,TPdg=0..] function use_degrading

    # gives weapons only during the countdown and round
    @e[type=armor_stand,TPStand,TPst=2..3,TPiwt=0..] function give_weapons

    # initializes the player
    @a[gSA=1,m=2] TPpl + 0
    @a[gSA=1,TPpl=0,m=2] function reset_player

    # calculates what happens when a carrot on a stick is used
    @a[gSA=1,gCS=1..,m=2] function use_carrot_stick

    # gives resistance to all, even spectators
    effect @a[gSA=1,m=2,TPpl=0..] $(Effects.resist) 3 10 true

    # displays the player count during countdown and round
    @e[type=armor_stand,TPStand] function display_players
    @a[gSA=1,m=2,TPpl=1..] function display_status

    # @e[type=armor_stand,TPStand,TPst=0] function wait_for_start
    @e[type=armor_stand,TPStand,TPst=1] function start_round
    @e[type=armor_stand,TPStand,TPst=2] function countdown
    @e[type=armor_stand,TPStand,TPst=3] function during_round
    @e[type=armor_stand,TPStand,TPst=4] function stop_round
    
    @a[m=2,TPpl=2,gDE=1..] function reset_player
    @a[$(select_spawn),m=2,TPpl=2] function reset_player

!mfunc term
    $(floo_event.cmd_term())

    @a[m=2,TPpl=1..] function full_reset_player

    $(OBJECTIVES.cmd_term())
    $(TEAMS.cmd_term())

    # sets the mid ball
    $(activate_struct_mid.setblock("redstone_block"))
    $(activate_struct_mid.setblock("air"))
    
    kill @e[TPEntity]


# Used on the TPStand when the round is starting
!mfunc start_round
    # initializes the weapons if there is nothing to initialize it with
    @s[TPiwt=-1] TPiwt = 0

    @s TPti = 120
    @s TPst = 2
    @a[gSA=1,m=2,TPpl=1] function add_to_round


# Used on the TPStand during the countdown
!mfunc countdown
    @s[TPti=1..] TPti - 1

    # displays the countdown
    ScOP Countdown TPcl = @s TPti
    ScOP Countdown TPcl /= 20 constants
    Countdown TPcl + 1
    ScOP Countdown TP = Countdown TPcl

    # gives weakness
    effect @a[gSA=1,m=2,TPpl=2] $(Effects.weak) 3 100 true

    @s[TPti=100] tellraw @a $(event.begin(r'{"text":"5","color":"yellow","bold":"true"}'))
    @s[TPti=100] @a playsound $(Sounds.pling) voice @s ~ ~ ~ 0.5

    @s[TPti=80] tellraw @a $(event.begin(r'{"text":"4","color":"yellow","bold":"true"}'))
    @s[TPti=80] @a playsound $(Sounds.pling) voice @s ~ ~ ~ 1

    @s[TPti=60] tellraw @a $(event.begin(r'{"text":"3","color":"yellow","bold":"true"}'))
    @s[TPti=60] @a playsound $(Sounds.pling) voice @s ~ ~ ~ 1.5

    @s[TPti=40] tellraw @a $(event.begin(r'{"text":"2","color":"yellow","bold":"true"}'))
    @s[TPti=40] @a playsound $(Sounds.pling) voice @s ~ ~ ~ 2
    
    @s[TPti=20] tellraw @a $(event.begin(r'{"text":"1","color":"yellow","bold":"true"}'))
    @s[TPti=20] @a playsound $(Sounds.pling) voice @s ~ ~ ~ 2

    @s[TPti=0] function end_countdown
    

# Used on the TPStand to stop the countdown
!mfunc end_countdown
    @a playsound $(Sounds.wither) voice @s
    tellraw @a $(event.begin(r'{"text":"Begin!","color":"green","bold":"true"}'))
    Countdown reset TP
    $(mid_ball_region.fill("air"))
    @s TPst = 3

    # removes weakness
    effect @a[gSA=1,m=2,TPpl=2] $(Effects.weak) 0 0 true

!mfunc during_round
    @s TPti + 1
    ScOP Time_Elapsed TPcl = @s TPti
    ScOP Time_Elapsed TPcl /= 20 constants
    ScOP Time_Elapsed TP = Time_Elapsed TPcl
    
    # teleports backwards towards the arena from the walls
    tp @a[$(select_walls_west),m=2,TPpl=2] $(tp_to_east)
    tp @a[$(select_walls_east),m=2,TPpl=2] $(tp_to_west)
    tp @a[$(select_walls_north),m=2,TPpl=2] $(tp_to_south)
    tp @a[$(select_walls_south),m=2,TPpl=2] $(tp_to_north)

    # if they somehow get above the mid ball
    tp @a[$(mid_ball_upper_select),m=2,TPpl=1] ~ ~-8 ~

    # kills all players on the floor
    kill @a[$(floor_select),m=2,TPpl=2]

    # kills all players on the pillar
    kill @a[$(on_pillar_select),m=2,TPpl=2]
    kill @a[$(pillar_sides_select),m=2,TPpl=2]


!mfunc stop_round
    @a[gSA=1,m=2,TPpl=2] function full_reset_player
    
    # sets the mid ball
    $(activate_struct_mid.setblock("redstone_block"))
    $(activate_struct_mid.setblock("air"))
    
    Countdown reset TP
    Time_Elapsed reset TP
    Players reset TP
    
    @s TPst = 0



!mfunc reset_player
    clear @s
    $(event.cmd_book("@s[EC=0]"))

    @s[TPpl=0] function give_items_in_lobby
    @s[TPpl=2] function give_items_in_lobby

    effect @s clear
    effect @s $(Effects.hp) 1 100 true
    xp -1000L @s

    join TP @s
    @s TPpl = 1


# Used on players to reset them fully
!mfunc full_reset_player
    function reset_player
    $(event.cmd_spawn())


# used on players to give them the necessary carrot sticks
# when they are in the lobby area
!mfunc give_items_in_lobby
    clear @s carrot_on_a_stick
    replaceitem entity @s slot.hotbar.0 carrot_on_a_stick 1 0 $(cs_to_spec_tags)
    replaceitem entity @s slot.hotbar.1 carrot_on_a_stick 1 0 $(cs_toggle_tags)

# used on players to give them the necessary carrot sticks
# when they are in the lobby area
!mfunc give_items_in_spectate
    clear @s carrot_on_a_stick
    replaceitem entity @s slot.hotbar.0 carrot_on_a_stick 1 0 $(cs_to_lobby_tags)
    replaceitem entity @s slot.hotbar.1 carrot_on_a_stick 1 0 $(cs_toggle_tags)

!mfunc use_carrot_stick
    # since it is global temp, set it to 0 to ensure you know what you're working with
    @s gTEMP = 0
    @s gTEMP = 1 {SelectedItem:{id:"minecraft:carrot_on_a_stick",Count:1b,tag:$(cs_to_lobby_tags)}}
    @s gTEMP = 2 {SelectedItem:{id:"minecraft:carrot_on_a_stick",Count:1b,tag:$(cs_to_spec_tags)}}
    @s gTEMP = 3 {SelectedItem:{id:"minecraft:carrot_on_a_stick",Count:1b,tag:$(cs_toggle_tags)}}

    # to lobby
    $(event.cmd_spawn("@s[gTEMP=1]"))
    @s[gTEMP=1] function give_items_in_lobby
     
    # to spectate
    tp @s[gTEMP=2] $(spectate_tp)
    @s[gTEMP=2] function give_items_in_spectate

    # toggle spectator
    # temporarily sets TPpl to 4 to be set back to 3 afterwards
    @s[gTEMP=3,TPpl=1] TPpl = 4
    @s[gTEMP=3,TPpl=3] TPpl = 1
    @s[gTEMP=3,TPpl=4] TPpl = 3


# Used on the TPStand to give players weapons
# when an input is detected with TPiwt=0..
# 0 = normal
# 1 = knockback 5
# 2 = porkchop
# 3 = bow & arrow
!mfunc give_weapons
    clear @a[gSA=1,m=2,TPpl=2]
    $(event.cmd_book("@a[gSA=1,m=2,TPpl=2]"))
    
    @s[TPiwt=0] give @a[gSA=1,m=2,TPpl=2] blaze_rod 1 0 {display:{Name:"A Poking Stick",Lore:["Knockback 3"]},ench:[{id:19,lvl:3}],HideFlags:127}
    @s[TPiwt=1] give @a[gSA=1,m=2,TPpl=2] blaze_rod 1 0 {display:{Name:"A Better Poking Stick",Lore:["Knockback 5"]},ench:[{id:19,lvl:3}],HideFlags:127}
    @s[TPiwt=2] give @a[gSA=1,m=2,TPpl=2] porkchop 1 0 {display:{Name:"The Holy Porkchop"},ench:[{id:19,lvl:9001}],HideFlags:127}
    @s[TPiwt=3] give @a[gSA=1,m=2,TPpl=2] bow 1 0 {display:{Name:"A Bow",Lore:["Punch 2"]},ench:[{id:49,lvl:2},{id:51,lvl:1}],HideFlags:127,Unbreakable:1}
    @s[TPiwt=3] give @a[gSA=1,m=2,TPpl=2] arrow 1 0 {display:{Name:"An Arrow"},ench:[{id:0,lvl:1}],HideFlags:127}

    @s TPiwt = -1


# Used on the TPStand to change the arena when
# an input is detected with TPiat=0..
!mfunc change_arena
    @s[TPiat=0] function arena_to_reg_small
    @s[TPiat=1] function arena_to_reg_large
    @s[TPiat=2] function arena_to_deg_small
    @s[TPiat=3] function arena_to_deg_large
    
    $(activate_load_plat.fill("redstone_block 0 replace air 0"))
    $(activate_load_plat.fill("air 0 replace redstone_block 0"))
    @s TPiat = -1


!mfunc arena_to_reg_small
    $structs_platform_load_with("reg_small")

!mfunc arena_to_reg_large
    $structs_platform_load_with("reg_large")

!mfunc arena_to_deg_small
    $structs_platform_load_with("deg_small")

!mfunc arena_to_deg_large
    $structs_platform_load_with("deg_large")



# used on players to add them to the round
!mfunc add_to_round
    function reset_player
    tp @s $(arena_tp)
    @s TPpl = 2


# used on the TPStand to display the number of players in the game
!mfunc display_players
    Players TPcl = 0
    @a[gSA=1,m=2,TPpl=2] Players TPcl + 1
    ScOP Players TP = Players TPcl


# used on the players to display whether they are going into the round or not
!mfunc display_status
    title @s[TPpl=1] actionbar {"text":"","extra":[{"text":"Will join next round: ","color":"yellow"},{"text":"True","color":"green"}]}
    title @s[TPpl=3] actionbar {"text":"","extra":[{"text":"Will join next round: ","color":"yellow"},{"text":"False","color":"red"}]}


!mfunc input_start_round
    @e[type=armor_stand,TPStand] TPst = 1

!mfunc input_reset_round
    @e[type=armor_stand,TPStand] TPst = 4


!mfunc input_set_reg_small
    @e[type=armor_stand,TPStand] TPiat = 0

!mfunc input_set_reg_large
    @e[type=armor_stand,TPStand] TPiat = 1

!mfunc input_set_deg_small
    @e[type=armor_stand,TPStand] TPiat = 2

!mfunc input_set_deg_large
    @e[type=armor_stand,TPStand] TPiat = 3


!mfunc book
    replaceitem entity @s slot.weapon.offhand written_book 1 0 {
        title:"$(event.full_name) Book",author:"eGO",pages:["[\"\",
            {\"text\":\"$(event.full_name) Settings\\\n\",\"bold\":\"true\"},
            {\"text\":\"\\\n\"},
            
            {\"text\":\"TP: \",\"color\":\"dark_gray\"},
            {\"text\":\"Start\",\"color\":\"dark_green\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(event.cmd_func("init"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Starts $(event.full_name) so it can be ran\",\"color\":\"green\"}}
            },
            {\"text\":\" / \",\"color\":\"gray\"},
            {\"text\":\"Stop\\\n\",\"color\":\"red\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(event.cmd_func("term"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Ends $(event.full_name)\",\"color\":\"red\"}}
            },
            
            {\"text\":\"Round: \",\"color\":\"dark_gray\"},
            {\"text\":\"Start\",\"color\":\"dark_green\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(event.cmd_func("input_start_round"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Starts an individual round\",\"color\":\"dark_green\"}}
            },
            {\"text\":\" / \",\"color\":\"gray\"},
            {\"text\":\"Reset\\\n\\\n\",\"color\":\"red\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(event.cmd_func("input_reset_round"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Stops an individual round\",\"color\":\"gold\"}}
            },

            {\"text\":\"Arena: \",\"color\":\"dark_gray\"},
            {\"text\":\"[RS]\",\"color\":\"gold\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(event.cmd_func("input_set_reg_small"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Regular, Small\",\"color\":\"gold\"}}
            },
            {\"text\":\" \",\"color\":\"gray\"},
            {\"text\":\"[RL]\",\"color\":\"gold\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(event.cmd_func("input_set_reg_large"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Regular, Large\",\"color\":\"gold\"}}
            },
            {\"text\":\" \",\"color\":\"gray\"},
            {\"text\":\"[DS]\",\"color\":\"gold\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(event.cmd_func("input_set_deg_small"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Degrading, Small\",\"color\":\"gold\"}}
            },
            {\"text\":\" \",\"color\":\"gray\"},
            {\"text\":\"[DL]\\\n\\\n\",\"color\":\"gold\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(event.cmd_func("input_set_deg_large"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Degrading, Large\",\"color\":\"gold\"}}
            },

            {\"text\":\"Weapon Settings\",\"bold\":\"true\",\"color\":\"black\"},
            {\"text\":\"\\\n\"},
            
            {\"text\":\"[0]\",\"color\":\"gold\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/scoreboard players set @e[type=armor_stand,tag=TPStand] TPiwt 0\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Knockback 3\",\"color\":\"gold\"}}
            },
            {\"text\":\" \",\"color\":\"gray\"},
            {\"text\":\"[1]\",\"color\":\"gold\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/scoreboard players set @e[type=armor_stand,tag=TPStand] TPiwt 1\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Knockback 5\",\"color\":\"gold\"}}
            },
            {\"text\":\" \",\"color\":\"gray\"},
            {\"text\":\"[2]\",\"color\":\"gold\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/scoreboard players set @e[type=armor_stand,tag=TPStand] TPiwt 2\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Porkchop\",\"color\":\"gold\"}}
            },
            {\"text\":\" \",\"color\":\"gray\"},
            {\"text\":\"[3]\",\"color\":\"gold\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/scoreboard players set @e[type=armor_stand,tag=TPStand] TPiwt 3\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Bow (punch 2)\",\"color\":\"gold\"}}
            }
        ]"
    ]}

    


