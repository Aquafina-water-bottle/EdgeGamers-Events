$py(
from lib.floo_network import PICTIONARY, FlooEvent
from lib.const_ints import CONST_INTS
from lib.scoreboard import OBJECTIVES, TEAMS
from lib.consts import *
from lib.coords import Coords


event = PICTIONARY
floo_event = FlooEvent(PICTIONARY)
initials = "PC"
select_all = event.select_all


# gets the guesses
with open("topics.txt") as file:
    lines = file.read().splitlines()

    # removes empty lines
    topic_list = [line for line in lines if line.strip()]
    print(topic_list)


# the first item in the file is the theme
theme = topic_list.pop(0)

# remove duplicates
topic_list = sorted(set(topic_list))

# fills the board
fill_board = Coords("130 18 192 130 6 168")

# teleport coords to the board so the player can draw
tp_to_draw = Coords("133 4 180 90 0")

# teleport back to the arena
tp_back = Coords("152 8 180 90 -30")

# redstone block to open the door with
activate_door = Coords("158 1 180")

shear_tags = r'''{
    display:{Name:"Special Shears!",Lore:["Destroys wool... Destroy ALL THE WOOL MUAHAHAHA"]},
    Unbreakable:1,CanDestroy:["minecraft:wool"],ench:[{id:32,lvl:32767}],HideFlags:127}'''

wool_tags = r'''{
    display:{Name:"Pictionary Wool!",Lore:["Wool for Pictionary.. Fear the Shears!!"]},
    CanPlaceOn:["minecraft:quartz_block"],CanDestroy:["minecraft:wool"],HideFlags:127}'''


wool_ids = [14, 1, 4, 5, 3, 10, 0, 12, 15, 2, 6, 8, 7, 9, 11]


OBJECTIVES.new_str("""
    . _ .
    ti _ Timer

    # 0 = waiting for the round to start
    # 1 = starting the round pulse
    # 2 = successfully found an artist pulse
    # 3 = during the round
    # 4 = stopping the round --> 0 pulse
    st _ State

    # 0 = initializing
    # 1 = in the game
    # 2 = artist
    pl _ Player List

    # used on the Guess clouds to randomly select one
    rd _ Random Choose

    cl _ Calculations

    # used on players to make sure they don't lose wool
    af _ Affected Items
    
""", initials=initials, display=event.full_name)

OBJECTIVES[initials].setdisplay("sidebar")

CONST_INTS.add_consts(20)
    
TEAMS.new_str("""
    . .
        color green
        friendlyfire false
        collisionRule never
   
    PCd_y Display yellow
        color yellow

""", initials=initials, display=event.full_name)
)


!mfunc init
    $(floo_event.cmd_init())
    $(OBJECTIVES.cmd_init())
    $(TEAMS.cmd_init())

    summon armor_stand ~ ~ ~ {Tags:["PCEntity","PCStand"],Invulnerable:1,NoGravity:1,Invisible:1,Marker:1b}

    $for(y_coord, topic in enumerate(topic_list))
    summon area_effect_cloud ~ ~$((y_coord+1) / 100) ~ {Duration:$(MAX_INT),CustomName:"$(topic)",Tags:["PCEntity","PCTopic"]}
    $endfor
    
    # Joins the PC team for some reason idk
    join PC @e[type=area_effect_cloud,PCTopic]
    join PCd_y Time_Elapsed
    
    # Initialize area effect clouds
    @e[type=area_effect_cloud,PCTopic] PCrd + 0
    
    # Removes redstone block at iron door
    setblock $(activate_door) air 0
    
    # says the nights' theme in the title and chat
    title @a title {"text":"Tonight's Theme:","color":"dark_aqua"}
    title @a subtitle {"text":"$(theme)","color":"gold","bold":"true"}
    tellraw @a $(event.begin(r'''{"text":"Tonight's Theme: ","color":"dark_aqua"},{"text":"''' + theme + r'","color":"gold","bold":"true"}'))

    # sets the host so they can never be chosen and they know the answer
    @s HOST = 0

    $(floo_event.cmd_post_init())
    

!mfunc main
    $(floo_event.cmd_main())

    # initializes the player
    @a[gSA=1,m=2] PCpl + 0
    @a[gSA=1,PCpl=0,m=2] function reset_player

    # kills any items dropped
    kill @e[$(select_all),type=item]

    # @e[type=armor_stand,PCStand,PCst=0] function wait_for_start
    @e[type=armor_stand,PCStand,PCst=1] function start_round
    @e[type=armor_stand,PCStand,PCst=3] function during_round
    @e[type=armor_stand,PCStand,PCst=4] function stop_round

    # pictionary status
    # @e[type=armor_stand,PCStand] PCti = 0
    
    @a[m=2,PCpl=2,gDE=1..] function reset_player


!mfunc term
    $(floo_event.cmd_term())

    @a[m=2,PCpl=1..] function full_reset_player

    $(OBJECTIVES.cmd_term())
    $(TEAMS.cmd_term())
    
    kill @e[PCEntity]

    # clears the board
    fill $(fill_board) air 0
    
    # closes the iron door
    setblock $(activate_door) redstone_block 0

    # resets host
    * reset HOST


# Used on the PCStand when the round is starting
!mfunc start_round
    function choose_artist

    # doesn't start the round if no artist has been chosen
    @s[PCst=1] tellraw @a[HOST=0] $(event.begin(r'{"text":"No artist was chosen","color":"red"}'))
    @s[PCst=1] PCst = 0
    @s[PCst=2] function finalize_start_round


# used on the PCStand when an artist has been chosen
!mfunc finalize_start_round
    # clears the board
    fill $(fill_board) air 0

    # Activates the timer (2 minutes)
    @s PCti = $(20 * 120)
    @s PCst = 3
    

!mfunc during_round
    # timer
    @s[PCti=1..] PCti - 1
    ScOP Time_Elapsed PCcl = @s PCti
    ScOP Time_Elapsed PCcl /= 20 constants
    ScOP Time_Elapsed PC = Time_Elapsed PCcl

    # updates the artist
    @a[gSA=1,m=2,PCpl=2] function update_artist

    # displays the topic to the host
    @a[gSA=1,HOST=0] function display_topic

    # displays the answer to the host


!mfunc stop_round
    @a[gSA=1,m=2,PCpl=2] function round_reset_player
    Time_Elapsed reset PC
    
    # display message
    title @a title {"text":"Round Over!","color":"dark_aqua","underlined":true}
    tellraw @a $(event.begin(r'''{"selector":"@e[type=area_effect_cloud,tag=PCTopic,score_PCrd_min=1,score_PCrd=1]","bold":"true"},
                             {"text":" is the answer!","color":"gray"}'''))
    
    # clear answer
    pictionary clear

    # removes the current answer
    kill @e[type=area_effect_cloud,PCTopic,PCrd=1]
    
    # general reset: clear, reset score, etc.
    @s PCst = 0


!mfunc reset_player
    clear @s
    $(event.cmd_book("@s[EC=0]"))

    effect @s clear
    effect @s $(Effects.hp) 1 100 true
    xp -1000L @s

    join PC @s
    @s PCpl = 1

    @s stats entity @s set AffectedItems @s PCaf
    @s PCaf = 0
    

# used on players to reset them for the round
!mfunc round_reset_player
    function reset_player
    tp @s $(tp_back)
    

# Used on players to reset them fully
!mfunc full_reset_player
    function reset_player
    $(event.cmd_spawn())


# used on the artist to replace their items and
# display the current topic
!mfunc update_artist
    function display_topic

    # gives back wool according to affected items
    $for(id in wool_ids)
    clear @s wool $(id) 0
    give @s[PCaf=0] wool 64 $(id) $(wool_tags)
    $endfor

    # same idea for shears
    clear @s shears -1 0
    give @s[PCaf=0] shears 1 0 $(shear_tags)


# used on the artist and the host to display the topic name
!mfunc display_topic
    title @s actionbar {"text":"","extra":[
        {"text":"Topic: ","color":"gray"},
        {"selector":"@e[type=area_effect_cloud,tag=PCTopic,score_PCrd_min=1,score_PCrd=1]"}
    ]}


# used on arbitrary to choose a random player to be added to the round
!mfunc choose_artist
    # sets all scores of PCcl to 1 except for the host
    @a PCcl = 0
    @a[gSA=1,m=2,PCpl=1] PCcl = 1
    @a[gSA=1,HOST=0] PCcl = 0

    # chooses from the PCcl to add to the round
    @r[PCcl=1] function add_to_round


# used on players to add them to the round
!mfunc add_to_round
    function reset_player
    @r[type=area_effect_cloud,PCTopic,PCrd=0] function choose_topic
    
    replaceitem entity @s slot.weapon.offhand shears 1 0 $(shear_tags)
    $for(index, id in enumerate(wool_ids))
    $if(index <= 8)

    # sets it to their hotbar for the first 8
    replaceitem entity @s slot.hotbar.$(index) wool 64 $(id) $(wool_tags)

    $else
    # gives the rest
    give @s wool 64 $(id) $(wool_tags)
    $endif

    $endfor
    
    tp @s $(tp_to_draw)
    @s pictionary set artist @p[r=0]
    
    # says who's the artist
    tellraw @a $(event.begin(r'{"selector":"@s"},{"text":" is the artist.","color":"gray"}'))

    # because this function implies an artist has been chosen, the PCStand
    # PCst = 2 to indicate that an artist has been successfully chosen
    @e[type=armor_stand,PCStand] PCst = 2
    

# used on the Topic Clouds to make it be the topic
!mfunc choose_topic
    # Selects a random area effect cloud
    @r[type=area_effect_cloud,PCTopic,PCrd=0] PCrd = 1

    # Sets the area effect cloud as the answer
    # teleports up to make sure that the selector gets the correct entity
    # tp @e[type=area_effect_cloud,PCTopic,PCrd=1] ~ ~0.1 ~
    @e[type=area_effect_cloud,PCTopic,PCrd=1] pictionary set entity @e[r=0]
    @e[type=area_effect_cloud,PCTopic,PCrd=1] say set @e[r=0]
    # tp @e[type=area_effect_cloud,PCTopic,PCrd=1] ~ ~-0.1 ~


!mfunc input_start_round
    @e[type=armor_stand,PCStand] PCst = 1

!mfunc input_stop_round
    @e[type=armor_stand,PCStand] PCst = 4


!mfunc book
    replaceitem entity @s slot.weapon.offhand written_book 1 0 {
        title:"$(event.full_name) Book",author:"eGO",pages:["[\"\",
            {\"text\":\"$(event.full_name) Settings\\\n\",\"bold\":\"true\"},
            {\"text\":\"\\\n\"},
            
            {\"text\":\"PC: \",\"color\":\"dark_gray\"},
            {\"text\":\"Start\",\"color\":\"dark_green\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(event.cmd_func("init"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Starts $(event.full_name) so it can be ran\",\"color\":\"green\"}}
            },
            {\"text\":\" / \",\"color\":\"gray\"},
            {\"text\":\"Stop\\\n\",\"color\":\"red\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(event.cmd_func("term"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Ends $(event.full_name)\",\"color\":\"red\"}}
            },
            
            {\"text\":\"Round: \",\"color\":\"dark_gray\"},
            {\"text\":\"Start\",\"color\":\"dark_green\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(event.cmd_func("input_start_round"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Starts an individual round\",\"color\":\"dark_green\"}}
            },
            {\"text\":\" / \",\"color\":\"gray\"},
            {\"text\":\"Reset\\\n\\\n\",\"color\":\"red\",
                \"clickEvent\":{\"action\":\"run_command\",\"value\":\"/$(event.cmd_func("input_stop_round"))\"},
                \"hoverEvent\":{\"action\":\"show_text\",\"value\":{\"text\":\"Stops an individual round\",\"color\":\"gold\"}}
            }
        ]"
    ]}
    